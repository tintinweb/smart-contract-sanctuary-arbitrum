// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/ContextUpgradeable.sol";
import "../proxy/utils/Initializable.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract OwnableUpgradeable is Initializable, ContextUpgradeable {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    function __Ownable_init() internal onlyInitializing {
        __Ownable_init_unchained();
    }

    function __Ownable_init_unchained() internal onlyInitializing {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }

    /**
     * @dev This empty reserved space is put in place to allow future versions to add new
     * variables without shifting down storage in the inheritance chain.
     * See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps
     */
    uint256[49] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (proxy/utils/Initializable.sol)

pragma solidity ^0.8.2;

import "../../utils/AddressUpgradeable.sol";

/**
 * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed
 * behind a proxy. Since proxied contracts do not make use of a constructor, it's common to move constructor logic to an
 * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer
 * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.
 *
 * The initialization functions use a version number. Once a version number is used, it is consumed and cannot be
 * reused. This mechanism prevents re-execution of each "step" but allows the creation of new initialization steps in
 * case an upgrade adds a module that needs to be initialized.
 *
 * For example:
 *
 * [.hljs-theme-light.nopadding]
 * ```
 * contract MyToken is ERC20Upgradeable {
 *     function initialize() initializer public {
 *         __ERC20_init("MyToken", "MTK");
 *     }
 * }
 * contract MyTokenV2 is MyToken, ERC20PermitUpgradeable {
 *     function initializeV2() reinitializer(2) public {
 *         __ERC20Permit_init("MyToken");
 *     }
 * }
 * ```
 *
 * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as
 * possible by providing the encoded function call as the `_data` argument to {ERC1967Proxy-constructor}.
 *
 * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure
 * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.
 *
 * [CAUTION]
 * ====
 * Avoid leaving a contract uninitialized.
 *
 * An uninitialized contract can be taken over by an attacker. This applies to both a proxy and its implementation
 * contract, which may impact the proxy. To prevent the implementation contract from being used, you should invoke
 * the {_disableInitializers} function in the constructor to automatically lock it when it is deployed:
 *
 * [.hljs-theme-light.nopadding]
 * ```
 * /// @custom:oz-upgrades-unsafe-allow constructor
 * constructor() {
 *     _disableInitializers();
 * }
 * ```
 * ====
 */
abstract contract Initializable {
    /**
     * @dev Indicates that the contract has been initialized.
     * @custom:oz-retyped-from bool
     */
    uint8 private _initialized;

    /**
     * @dev Indicates that the contract is in the process of being initialized.
     */
    bool private _initializing;

    /**
     * @dev Triggered when the contract has been initialized or reinitialized.
     */
    event Initialized(uint8 version);

    /**
     * @dev A modifier that defines a protected initializer function that can be invoked at most once. In its scope,
     * `onlyInitializing` functions can be used to initialize parent contracts. Equivalent to `reinitializer(1)`.
     */
    modifier initializer() {
        bool isTopLevelCall = !_initializing;
        require(
            (isTopLevelCall && _initialized < 1) || (!AddressUpgradeable.isContract(address(this)) && _initialized == 1),
            "Initializable: contract is already initialized"
        );
        _initialized = 1;
        if (isTopLevelCall) {
            _initializing = true;
        }
        _;
        if (isTopLevelCall) {
            _initializing = false;
            emit Initialized(1);
        }
    }

    /**
     * @dev A modifier that defines a protected reinitializer function that can be invoked at most once, and only if the
     * contract hasn't been initialized to a greater version before. In its scope, `onlyInitializing` functions can be
     * used to initialize parent contracts.
     *
     * `initializer` is equivalent to `reinitializer(1)`, so a reinitializer may be used after the original
     * initialization step. This is essential to configure modules that are added through upgrades and that require
     * initialization.
     *
     * Note that versions can jump in increments greater than 1; this implies that if multiple reinitializers coexist in
     * a contract, executing them in the right order is up to the developer or operator.
     */
    modifier reinitializer(uint8 version) {
        require(!_initializing && _initialized < version, "Initializable: contract is already initialized");
        _initialized = version;
        _initializing = true;
        _;
        _initializing = false;
        emit Initialized(version);
    }

    /**
     * @dev Modifier to protect an initialization function so that it can only be invoked by functions with the
     * {initializer} and {reinitializer} modifiers, directly or indirectly.
     */
    modifier onlyInitializing() {
        require(_initializing, "Initializable: contract is not initializing");
        _;
    }

    /**
     * @dev Locks the contract, preventing any future reinitialization. This cannot be part of an initializer call.
     * Calling this in the constructor of a contract will prevent that contract from being initialized or reinitialized
     * to any version. It is recommended to use this to lock implementation contracts that are designed to be called
     * through proxies.
     */
    function _disableInitializers() internal virtual {
        require(!_initializing, "Initializable: contract is initializing");
        if (_initialized < type(uint8).max) {
            _initialized = type(uint8).max;
            emit Initialized(type(uint8).max);
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (security/Pausable.sol)

pragma solidity ^0.8.0;

import "../utils/ContextUpgradeable.sol";
import "../proxy/utils/Initializable.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract PausableUpgradeable is Initializable, ContextUpgradeable {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    function __Pausable_init() internal onlyInitializing {
        __Pausable_init_unchained();
    }

    function __Pausable_init_unchained() internal onlyInitializing {
        _paused = false;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        _requireNotPaused();
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        _requirePaused();
        _;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Throws if the contract is paused.
     */
    function _requireNotPaused() internal view virtual {
        require(!paused(), "Pausable: paused");
    }

    /**
     * @dev Throws if the contract is not paused.
     */
    function _requirePaused() internal view virtual {
        require(paused(), "Pausable: not paused");
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }

    /**
     * @dev This empty reserved space is put in place to allow future versions to add new
     * variables without shifting down storage in the inheritance chain.
     * See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps
     */
    uint256[49] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (token/ERC1155/IERC1155.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165Upgradeable.sol";

/**
 * @dev Required interface of an ERC1155 compliant contract, as defined in the
 * https://eips.ethereum.org/EIPS/eip-1155[EIP].
 *
 * _Available since v3.1._
 */
interface IERC1155Upgradeable is IERC165Upgradeable {
    /**
     * @dev Emitted when `value` tokens of token type `id` are transferred from `from` to `to` by `operator`.
     */
    event TransferSingle(address indexed operator, address indexed from, address indexed to, uint256 id, uint256 value);

    /**
     * @dev Equivalent to multiple {TransferSingle} events, where `operator`, `from` and `to` are the same for all
     * transfers.
     */
    event TransferBatch(
        address indexed operator,
        address indexed from,
        address indexed to,
        uint256[] ids,
        uint256[] values
    );

    /**
     * @dev Emitted when `account` grants or revokes permission to `operator` to transfer their tokens, according to
     * `approved`.
     */
    event ApprovalForAll(address indexed account, address indexed operator, bool approved);

    /**
     * @dev Emitted when the URI for token type `id` changes to `value`, if it is a non-programmatic URI.
     *
     * If an {URI} event was emitted for `id`, the standard
     * https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[guarantees] that `value` will equal the value
     * returned by {IERC1155MetadataURI-uri}.
     */
    event URI(string value, uint256 indexed id);

    /**
     * @dev Returns the amount of tokens of token type `id` owned by `account`.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function balanceOf(address account, uint256 id) external view returns (uint256);

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {balanceOf}.
     *
     * Requirements:
     *
     * - `accounts` and `ids` must have the same length.
     */
    function balanceOfBatch(address[] calldata accounts, uint256[] calldata ids)
        external
        view
        returns (uint256[] memory);

    /**
     * @dev Grants or revokes permission to `operator` to transfer the caller's tokens, according to `approved`,
     *
     * Emits an {ApprovalForAll} event.
     *
     * Requirements:
     *
     * - `operator` cannot be the caller.
     */
    function setApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns true if `operator` is approved to transfer ``account``'s tokens.
     *
     * See {setApprovalForAll}.
     */
    function isApprovedForAll(address account, address operator) external view returns (bool);

    /**
     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.
     *
     * Emits a {TransferSingle} event.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - If the caller is not `from`, it must have been approved to spend ``from``'s tokens via {setApprovalForAll}.
     * - `from` must have a balance of tokens of type `id` of at least `amount`.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the
     * acceptance magic value.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes calldata data
    ) external;

    /**
     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {safeTransferFrom}.
     *
     * Emits a {TransferBatch} event.
     *
     * Requirements:
     *
     * - `ids` and `amounts` must have the same length.
     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the
     * acceptance magic value.
     */
    function safeBatchTransferFrom(
        address from,
        address to,
        uint256[] calldata ids,
        uint256[] calldata amounts,
        bytes calldata data
    ) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20Upgradeable {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library AddressUpgradeable {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly
                /// @solidity memory-safe-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;
import "../proxy/utils/Initializable.sol";

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract ContextUpgradeable is Initializable {
    function __Context_init() internal onlyInitializing {
    }

    function __Context_init_unchained() internal onlyInitializing {
    }
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }

    /**
     * @dev This empty reserved space is put in place to allow future versions to add new
     * variables without shifting down storage in the inheritance chain.
     * See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps
     */
    uint256[50] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165Upgradeable {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/token/ERC1155/IERC1155Upgradeable.sol";

interface IConsumable is IERC1155Upgradeable {

    function mint(address _to, uint256 _id, uint256 _amount) external;

    function burn(address account, uint256 id, uint256 value) external;

    function burnBatch(address account, uint256[] memory ids, uint256[] memory values) external;

    function adminSafeTransferFrom(address _from, address _to, uint256 _id, uint256 _amount) external;

    function adminSafeBatchTransferFrom(address _from, address _to, uint256[] calldata _ids, uint256[] calldata _amounts) external;

    function adminBurn(address account, uint256 id, uint256 value) external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/token/ERC20/IERC20Upgradeable.sol";

interface ICorruption is IERC20Upgradeable {
    function burn(address _account, uint256 _amount) external;
    function setCorruptionStreamBoost(address _account, uint32 _boost) external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface ICorruptionCrypts {
    function ownerOf(uint64 _legionSquadId) external view returns(address);

    function isLegionSquadActive(uint64 _legionSquadId) external view returns(bool);

    function legionIdsForLegionSquad(uint64 _legionSquadId) external view returns(uint32[] memory);

    function currentRoundId() external view returns(uint256);

    function getRoundStartTime() external view returns(uint256);

    function lastRoundEnteredTemple(uint64 _legionSquadId) external view returns(uint32);
}

//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

import "./CorruptionCryptsRewardsContracts.sol";

contract CorruptionCryptsRewards is Initializable, CorruptionCryptsRewardsContracts {

    function initialize() external initializer {
        CorruptionCryptsRewardsContracts.__CorruptionCryptsRewardsContracts_init();
    }

    function onLegionsArrivedAtHarvester(
        address _harvesterAddress,
        uint32[] calldata _legionIds)
    external
    whenNotPaused
    onlyCrypts
    {
        uint24 _totalCorruptionDiversionPoints = 0;

        for(uint256 i = 0; i < _legionIds.length; i++) {
            uint32 _legionId = _legionIds[i];

            LegionMetadata memory _metadata = legionMetadataStore.metadataForLegion(_legionId);

            // Lookup the diversion points for this legion.
            _totalCorruptionDiversionPoints += generationToRarityToCorruptionDiversion[_metadata.legionGeneration][_metadata.legionRarity];
        }

        harvesterCorruptionInfo.totalCorruptionDiversionPoints += _totalCorruptionDiversionPoints;

        bool _foundMatchingHarvester = false;
        for(uint256 i = 0; i < activeHarvesterInfos.length; i++) {
            if(activeHarvesterInfos[i].harvesterAddress == _harvesterAddress) {
                _foundMatchingHarvester = true;
                _addCorruptionPointsForHarvesterIndex(i, _totalCorruptionDiversionPoints);
            }
        }

        require(_foundMatchingHarvester, "Could not find matching, active harvester");

        _calculateAndAdjustHarvesterBoosts();
    }

    function onNewRoundBegin(address[] memory activeHarvesterAddresses)
    external
    onlyCrypts
    {
        // Clear out all accumulated point totals and set boosts back to 0.
        delete harvesterCorruptionInfo;

        // Reset boosts BEFORE setting the new active harvesters
        _calculateAndAdjustHarvesterBoosts();

        delete activeHarvesterInfos;
        for(uint256 i = 0; i < activeHarvesterAddresses.length; i++) {
            activeHarvesterInfos.push(HarvesterInfo(activeHarvesterAddresses[i], 0));
        }
    }

    function craftCorruption(
        CraftCorruptionParams[] calldata _params)
    external
    whenNotPaused
    onlyEOA
    {
        require(_params.length > 0, "Bad array length");

        for(uint256 i = 0; i < _params.length; i++) {
            _craftCorruption(_params[i]);
        }
    }

    function _craftCorruption(CraftCorruptionParams calldata _params) private {
        // Confirm that the indexes are good and the user owns this legion squad and legion squad is active
        require(corruptionCrypts.ownerOf(_params.legionSquadId) == msg.sender, "Not your squad");
        require(corruptionCrypts.isLegionSquadActive(_params.legionSquadId), "Squad is not active");
        require(_params.legionIdIndexes.length > 0, "Bad length");

        // Confirm that the user has reached the temple in this round OR has reached the temple last round and
        // hasn't move yet + its been less than X minutes since the time reset
        uint32 _currentRound = uint32(corruptionCrypts.currentRoundId());
        uint256 _roundStartTime = corruptionCrypts.getRoundStartTime();
        uint32 _lastRoundInTemple = corruptionCrypts.lastRoundEnteredTemple(_params.legionSquadId);

        require(_currentRound == _lastRoundInTemple
            || (_currentRound > 1 && _currentRound - 1 == _lastRoundInTemple && _roundStartTime + roundResetTimeAllowance >= block.timestamp),
            "Legion Squad cannot craft");

        uint32[] memory _legionIds = corruptionCrypts.legionIdsForLegionSquad(_params.legionSquadId);

        uint256 _totalPoolBalance = corruption.balanceOf(address(this));
        uint256 _totalCorruptionToTransfer;

        for(uint256 i = 0; i < _params.legionIdIndexes.length; i++) {
            uint16 _legionIdIndex = _params.legionIdIndexes[i];
            require(_legionIdIndex < _legionIds.length, "Bad legion index");
            uint32 _legionId = _legionIds[_legionIdIndex];
            // Confirm that the legion has not already crafted for this round
            require(legionIdToInfo[_legionId].lastRoundCrafted < _lastRoundInTemple, "Legion already crafted");

            legionIdToInfo[_legionId].lastRoundCrafted = _lastRoundInTemple;

            LegionMetadata memory _legionMetadata = legionMetadataStore.metadataForLegion(_legionId);
            require(_legionMetadata.legionGeneration == LegionGeneration.GENESIS
                || _legionMetadata.craftLevel >= minimumCraftLevelForAuxCorruption, "Craft level too low");

            uint32 _percentClaimed = generationToRarityToPercentOfPoolClaimed[_legionMetadata.legionGeneration][_legionMetadata.legionRarity];

            uint256 _claimedAmount = (uint256(_percentClaimed) * _totalPoolBalance) / 100_000;
            _totalCorruptionToTransfer += _claimedAmount;
            _totalPoolBalance -= _claimedAmount;

            emit LegionCraftedCorruption(_legionId, _lastRoundInTemple, _claimedAmount);
        }

        corruption.transfer(msg.sender, _totalCorruptionToTransfer);
        consumable.adminBurn(msg.sender, MALEVOLENT_PRISM_ID, _params.legionIdIndexes.length * malevolentPrismsPerCraft);
    }

    // Based on the current diversion points, calculates the diversion to each harvester and updates the boost for each.
    //
    function _calculateAndAdjustHarvesterBoosts() private {
        // Adjust boosts.
        for(uint8 i = 0; i < activeHarvesterInfos.length; i++) {
            uint24 _corruptionPoints = _corruptionPointsForHarvesterIndex(i);
            uint32 _boost = _calculateHarvesterBoost(_corruptionPoints, harvesterCorruptionInfo.totalCorruptionDiversionPoints);
            corruption.setCorruptionStreamBoost(activeHarvesterInfos[i].harvesterAddress, _boost);
        }
    }

    modifier onlyCrypts() {
        require(msg.sender == address(corruptionCrypts), "Only crypts can call.");

        _;
    }

    function _addCorruptionPointsForHarvesterIndex(uint256 _harvesterIndex, uint24 _totalCorruptionPointsToAdd) private {
        if(_harvesterIndex == 0) {
            harvesterCorruptionInfo.harvester1CorruptionPoints += _totalCorruptionPointsToAdd;
        } else if(_harvesterIndex == 1) {
            harvesterCorruptionInfo.harvester2CorruptionPoints += _totalCorruptionPointsToAdd;
        } else if(_harvesterIndex == 2) {
            harvesterCorruptionInfo.harvester3CorruptionPoints += _totalCorruptionPointsToAdd;
        } else {
            revert("More than 3 active harvester. Need to upgrade CorruptionCryptsRewards");
        }
    }

    function _corruptionPointsForHarvesterIndex(uint256 _harvesterIndex) private view returns(uint24 _corruptionPoints) {
        if(_harvesterIndex == 0) {
            _corruptionPoints = harvesterCorruptionInfo.harvester1CorruptionPoints;
        } else if(_harvesterIndex == 1) {
            _corruptionPoints = harvesterCorruptionInfo.harvester2CorruptionPoints;
        } else if(_harvesterIndex == 2) {
            _corruptionPoints = harvesterCorruptionInfo.harvester3CorruptionPoints;
        } else {
            revert("More than 3 active harvester. Need to upgrade CorruptionCryptsRewards");
        }
    }

    function _calculateHarvesterBoost(uint24 _totalForHarvester, uint24 _total) private pure returns(uint32) {
        if(_totalForHarvester == 0) {
            return 0;
        }
        return uint32((100_000 * uint256(_totalForHarvester)) / uint256(_total));
    }
}

struct CraftCorruptionParams {
    uint64 legionSquadId;
    uint8[] legionIdIndexes;
}

//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

import "./CorruptionCryptsRewardsState.sol";

abstract contract CorruptionCryptsRewardsContracts is Initializable, CorruptionCryptsRewardsState {

    function __CorruptionCryptsRewardsContracts_init() internal initializer {
        CorruptionCryptsRewardsState.__CorruptionCryptsRewardsState_init();
    }

    function setContracts(
        address _corruptionAddress,
        address _legionMetadataStoreAddress,
        address _corruptionCryptsAddress,
        address _consumableAddress)
    external onlyAdminOrOwner
    {
        corruption = ICorruption(_corruptionAddress);
        legionMetadataStore = ILegionMetadataStore(_legionMetadataStoreAddress);
        corruptionCrypts = ICorruptionCrypts(_corruptionCryptsAddress);
        consumable = IConsumable(_consumableAddress);
    }

    modifier contractsAreSet() {
        require(areContractsSet(), "CorruptionCryptsRewards: Contracts aren't set");
        _;
    }

    function areContractsSet() public view returns(bool) {
        return address(corruption) != address(0)
            && address(legionMetadataStore) != address(0)
            && address(consumable) != address(0)
            && address(corruptionCrypts) != address(0);
    }
}

//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

import "./ICorruptionCryptsRewards.sol";
import "../../shared/AdminableUpgradeable.sol";
import "../corruption/ICorruption.sol";
import "../legionmetadatastore/ILegionMetadataStore.sol";
import "../corruptioncrypts/ICorruptionCrypts.sol";
import "../consumable/IConsumable.sol";

abstract contract CorruptionCryptsRewardsState is Initializable, ICorruptionCryptsRewards, AdminableUpgradeable {

    event RoundResetTimeAllowanceSet(uint256 roundResetTimeAllowance);
    event MinimumCraftLevelForAuxCorruptionSet(uint256 craftLevel);
    event MalevolentPrismsPerCraftSet(uint256 malevolentPrisms);
    event LegionPercentOfPoolClaimedChanged(LegionGeneration generation, LegionRarity rarity, uint32 percentOfPool);

    event LegionCraftedCorruption(uint32 _legionId, uint32 _roundCraftedFor, uint256 _amountCrafted);

    uint256 constant MALEVOLENT_PRISM_ID = 15;

    ICorruption public corruption;
    ILegionMetadataStore public legionMetadataStore;
    ICorruptionCrypts public corruptionCrypts;
    IConsumable public consumable;

    HarvesterCorruptionInfo public harvesterCorruptionInfo;

    uint256 public roundResetTimeAllowance;
    uint256 public minimumCraftLevelForAuxCorruption;
    uint256 public malevolentPrismsPerCraft;

    mapping(LegionGeneration => mapping(LegionRarity => uint24)) public generationToRarityToCorruptionDiversion;
    mapping(LegionGeneration => mapping(LegionRarity => uint32)) public generationToRarityToPercentOfPoolClaimed;
    HarvesterInfo[] public activeHarvesterInfos;

    mapping(uint32 => LegionInfo) public legionIdToInfo;

    function __CorruptionCryptsRewardsState_init() internal initializer {
        AdminableUpgradeable.__Adminable_init();

        roundResetTimeAllowance = 30 minutes;
        emit RoundResetTimeAllowanceSet(roundResetTimeAllowance);

        minimumCraftLevelForAuxCorruption = 3;
        emit MinimumCraftLevelForAuxCorruptionSet(minimumCraftLevelForAuxCorruption);

        malevolentPrismsPerCraft = 1;
        emit MalevolentPrismsPerCraftSet(malevolentPrismsPerCraft);

        generationToRarityToCorruptionDiversion[LegionGeneration.GENESIS][LegionRarity.LEGENDARY] = 600;
        generationToRarityToCorruptionDiversion[LegionGeneration.GENESIS][LegionRarity.RARE] = 400;
        generationToRarityToCorruptionDiversion[LegionGeneration.GENESIS][LegionRarity.UNCOMMON] = 200;
        generationToRarityToCorruptionDiversion[LegionGeneration.GENESIS][LegionRarity.SPECIAL] = 150;
        generationToRarityToCorruptionDiversion[LegionGeneration.GENESIS][LegionRarity.COMMON] = 100;
        generationToRarityToCorruptionDiversion[LegionGeneration.AUXILIARY][LegionRarity.RARE] = 40;
        generationToRarityToCorruptionDiversion[LegionGeneration.AUXILIARY][LegionRarity.UNCOMMON] = 30;
        generationToRarityToCorruptionDiversion[LegionGeneration.AUXILIARY][LegionRarity.COMMON] = 20;

        _setLegionPercentOfPoolClaimed(LegionGeneration.GENESIS, LegionRarity.LEGENDARY, 3500);
        _setLegionPercentOfPoolClaimed(LegionGeneration.GENESIS, LegionRarity.RARE, 2500);
        _setLegionPercentOfPoolClaimed(LegionGeneration.GENESIS, LegionRarity.UNCOMMON, 1500);
        _setLegionPercentOfPoolClaimed(LegionGeneration.GENESIS, LegionRarity.SPECIAL, 1250);
        _setLegionPercentOfPoolClaimed(LegionGeneration.GENESIS, LegionRarity.COMMON, 1000);
        _setLegionPercentOfPoolClaimed(LegionGeneration.AUXILIARY, LegionRarity.RARE, 550);
        _setLegionPercentOfPoolClaimed(LegionGeneration.AUXILIARY, LegionRarity.UNCOMMON, 530);
        _setLegionPercentOfPoolClaimed(LegionGeneration.AUXILIARY, LegionRarity.COMMON, 500);
    }

    function _setLegionPercentOfPoolClaimed(
        LegionGeneration _generation,
        LegionRarity _rarity,
        uint32 _percent)
    private
    {
        generationToRarityToPercentOfPoolClaimed[_generation][_rarity] = _percent;
        emit LegionPercentOfPoolClaimedChanged(_generation, _rarity, _percent);
    }
}

// Not safe to update struct past 1 slot
//
struct HarvesterInfo {
    address harvesterAddress;
    uint96 emptySpace;
}

// Instead of storing the corruption points in a mapping, we will pack all the points info together into one struct. This will support all 9 potentional harvesters
// and keep balance writes down to 1 storage slot.
struct HarvesterCorruptionInfo {
    uint24 totalCorruptionDiversionPoints;
    uint24 harvester1CorruptionPoints;
    uint24 harvester2CorruptionPoints;
    uint24 harvester3CorruptionPoints;
    uint160 emptySpace;
}

struct LegionInfo {
    uint32 lastRoundCrafted;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface ICorruptionCryptsRewards {
    function onLegionsArrivedAtHarvester(
        address _harvesterAddress,
        uint32[] calldata _legionIds
    ) external;

    function onNewRoundBegin(
        address[] memory activeHarvesterAddresses
    ) external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "./LegionMetadataStoreState.sol";

interface ILegionMetadataStore {
    // Sets the intial metadata for a token id.
    // Admin only.
    function setInitialMetadataForLegion(address _owner, uint256 _tokenId, LegionGeneration _generation, LegionClass _class, LegionRarity _rarity, uint256 _oldId) external;

    // Increases the quest level by one. It is up to the calling contract to regulate the max quest level. No validation.
    // Admin only.
    function increaseQuestLevel(uint256 _tokenId) external;

    // Increases the craft level by one. It is up to the calling contract to regulate the max craft level. No validation.
    // Admin only.
    function increaseCraftLevel(uint256 _tokenId) external;

    // Increases the rank of the given constellation to the given number. It is up to the calling contract to regulate the max constellation rank. No validation.
    // Admin only.
    function increaseConstellationRank(uint256 _tokenId, Constellation _constellation, uint8 _to) external;

    // Returns the metadata for the given legion.
    function metadataForLegion(uint256 _tokenId) external view returns(LegionMetadata memory);

    // Returns the tokenUri for the given token.
    function tokenURI(uint256 _tokenId) external view returns(string memory);
}

// As this will likely change in the future, this should not be used to store state, but rather
// as parameters and return values from functions.
struct LegionMetadata {
    LegionGeneration legionGeneration;
    LegionClass legionClass;
    LegionRarity legionRarity;
    uint8 questLevel;
    uint8 craftLevel;
    uint8[6] constellationRanks;
    uint256 oldId;
}

enum Constellation {
    FIRE,
    EARTH,
    WIND,
    WATER,
    LIGHT,
    DARK
}

enum LegionRarity {
    LEGENDARY,
    RARE,
    SPECIAL,
    UNCOMMON,
    COMMON,
    RECRUIT
}

enum LegionClass {
    RECRUIT,
    SIEGE,
    FIGHTER,
    ASSASSIN,
    RANGED,
    SPELLCASTER,
    RIVERMAN,
    NUMERAIRE,
    ALL_CLASS,
    ORIGIN
}

enum LegionGeneration {
    GENESIS,
    AUXILIARY,
    RECRUIT
}

//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

import "../../shared/AdminableUpgradeable.sol";
import "./ILegionMetadataStore.sol";

abstract contract LegionMetadataStoreState is Initializable, AdminableUpgradeable {

    event LegionQuestLevelUp(uint256 indexed _tokenId, uint8 _questLevel);
    event LegionCraftLevelUp(uint256 indexed _tokenId, uint8 _craftLevel);
    event LegionConstellationRankUp(uint256 indexed _tokenId, Constellation indexed _constellation, uint8 _rank);
    event LegionCreated(address indexed _owner, uint256 indexed _tokenId, LegionGeneration _generation, LegionClass _class, LegionRarity _rarity);

    mapping(uint256 => LegionGeneration) internal idToGeneration;
    mapping(uint256 => LegionClass) internal idToClass;
    mapping(uint256 => LegionRarity) internal idToRarity;
    mapping(uint256 => uint256) internal idToOldId;
    mapping(uint256 => uint8) internal idToQuestLevel;
    mapping(uint256 => uint8) internal idToCraftLevel;
    mapping(uint256 => uint8[6]) internal idToConstellationRanks;

    mapping(LegionGeneration => mapping(LegionClass => mapping(LegionRarity => mapping(uint256 => string)))) internal _genToClassToRarityToOldIdToUri;

    function __LegionMetadataStoreState_init() internal initializer {
        AdminableUpgradeable.__Adminable_init();
    }
}

//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

import "./UtilitiesUpgradeable.sol";

// Do not add state to this contract.
//
contract AdminableUpgradeable is UtilitiesUpgradeable {

    mapping(address => bool) private admins;

    function __Adminable_init() internal initializer {
        UtilitiesUpgradeable.__Utilities__init();
    }

    function addAdmin(address _address) external onlyOwner {
        admins[_address] = true;
    }

    function addAdmins(address[] calldata _addresses) external onlyOwner {
        for(uint256 i = 0; i < _addresses.length; i++) {
            admins[_addresses[i]] = true;
        }
    }

    function removeAdmin(address _address) external onlyOwner {
        admins[_address] = false;
    }

    function removeAdmins(address[] calldata _addresses) external onlyOwner {
        for(uint256 i = 0; i < _addresses.length; i++) {
            admins[_addresses[i]] = false;
        }
    }

    function setPause(bool _shouldPause) external onlyAdminOrOwner {
        if(_shouldPause) {
            _pause();
        } else {
            _unpause();
        }
    }

    function isAdmin(address _address) public view returns(bool) {
        return admins[_address];
    }

    modifier onlyAdmin() {
        require(admins[msg.sender], "Not admin");
        _;
    }

    modifier onlyAdminOrOwner() {
        require(admins[msg.sender] || isOwner(), "Not admin or owner");
        _;
    }

    uint256[50] private __gap;
}

//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/security/PausableUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

contract UtilitiesUpgradeable is Initializable, OwnableUpgradeable, PausableUpgradeable {

    function __Utilities__init() internal initializer {
        OwnableUpgradeable.__Ownable_init();
        PausableUpgradeable.__Pausable_init();

        _pause();
    }

    modifier nonZeroAddress(address _address) {
        require(address(0) != _address, "0 address");
        _;
    }

    modifier nonZeroLength(uint[] memory _array) {
        require(_array.length > 0, "Empty array");
        _;
    }

    modifier lengthsAreEqual(uint[] memory _array1, uint[] memory _array2) {
        require(_array1.length == _array2.length, "Unequal lengths");
        _;
    }

    modifier onlyEOA() {
        /* solhint-disable avoid-tx-origin */
        require(msg.sender == tx.origin, "No contracts");
        _;
    }

    function isOwner() internal view returns(bool) {
        return owner() == msg.sender;
    }

    function compareStrings(string memory a, string memory b) internal pure returns (bool) {
        return (keccak256(abi.encodePacked((a))) == keccak256(abi.encodePacked((b))));
    }
}