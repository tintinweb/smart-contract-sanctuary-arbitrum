// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (proxy/utils/Initializable.sol)

pragma solidity ^0.8.0;

import "../../utils/AddressUpgradeable.sol";

/**
 * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed
 * behind a proxy. Since proxied contracts do not make use of a constructor, it's common to move constructor logic to an
 * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer
 * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.
 *
 * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as
 * possible by providing the encoded function call as the `_data` argument to {ERC1967Proxy-constructor}.
 *
 * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure
 * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.
 *
 * [CAUTION]
 * ====
 * Avoid leaving a contract uninitialized.
 *
 * An uninitialized contract can be taken over by an attacker. This applies to both a proxy and its implementation
 * contract, which may impact the proxy. To initialize the implementation contract, you can either invoke the
 * initializer manually, or you can include a constructor to automatically mark it as initialized when it is deployed:
 *
 * [.hljs-theme-light.nopadding]
 * ```
 * /// @custom:oz-upgrades-unsafe-allow constructor
 * constructor() initializer {}
 * ```
 * ====
 */
abstract contract Initializable {
    /**
     * @dev Indicates that the contract has been initialized.
     */
    bool private _initialized;

    /**
     * @dev Indicates that the contract is in the process of being initialized.
     */
    bool private _initializing;

    /**
     * @dev Modifier to protect an initializer function from being invoked twice.
     */
    modifier initializer() {
        // If the contract is initializing we ignore whether _initialized is set in order to support multiple
        // inheritance patterns, but we only do this in the context of a constructor, because in other contexts the
        // contract may have been reentered.
        require(_initializing ? _isConstructor() : !_initialized, "Initializable: contract is already initialized");

        bool isTopLevelCall = !_initializing;
        if (isTopLevelCall) {
            _initializing = true;
            _initialized = true;
        }

        _;

        if (isTopLevelCall) {
            _initializing = false;
        }
    }

    /**
     * @dev Modifier to protect an initialization function so that it can only be invoked by functions with the
     * {initializer} modifier, directly or indirectly.
     */
    modifier onlyInitializing() {
        require(_initializing, "Initializable: contract is not initializing");
        _;
    }

    function _isConstructor() private view returns (bool) {
        return !AddressUpgradeable.isContract(address(this));
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library AddressUpgradeable {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (interfaces/draft-IERC1822.sol)

pragma solidity ^0.8.0;

/**
 * @dev ERC1822: Universal Upgradeable Proxy Standard (UUPS) documents a method for upgradeability through a simplified
 * proxy whose upgrades are fully controlled by the current implementation.
 */
interface IERC1822Proxiable {
    /**
     * @dev Returns the storage slot that the proxiable contract assumes is being used to store the implementation
     * address.
     *
     * IMPORTANT: A proxy pointing at a proxiable contract should not be considered proxiable itself, because this risks
     * bricking a proxy that upgrades to it, by delegating to itself until out of gas. Thus it is critical that this
     * function revert if invoked through a proxy.
     */
    function proxiableUUID() external view returns (bytes32);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (proxy/beacon/IBeacon.sol)

pragma solidity ^0.8.0;

/**
 * @dev This is the interface that {BeaconProxy} expects of its beacon.
 */
interface IBeacon {
    /**
     * @dev Must return an address that can be used as a delegate call target.
     *
     * {BeaconProxy} will check that this address is a contract.
     */
    function implementation() external view returns (address);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (proxy/ERC1967/ERC1967Upgrade.sol)

pragma solidity ^0.8.2;

import "../beacon/IBeacon.sol";
import "../../interfaces/draft-IERC1822.sol";
import "../../utils/Address.sol";
import "../../utils/StorageSlot.sol";

/**
 * @dev This abstract contract provides getters and event emitting update functions for
 * https://eips.ethereum.org/EIPS/eip-1967[EIP1967] slots.
 *
 * _Available since v4.1._
 *
 * @custom:oz-upgrades-unsafe-allow delegatecall
 */
abstract contract ERC1967Upgrade {
    // This is the keccak-256 hash of "eip1967.proxy.rollback" subtracted by 1
    bytes32 private constant _ROLLBACK_SLOT = 0x4910fdfa16fed3260ed0e7147f7cc6da11a60208b5b9406d12a635614ffd9143;

    /**
     * @dev Storage slot with the address of the current implementation.
     * This is the keccak-256 hash of "eip1967.proxy.implementation" subtracted by 1, and is
     * validated in the constructor.
     */
    bytes32 internal constant _IMPLEMENTATION_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;

    /**
     * @dev Emitted when the implementation is upgraded.
     */
    event Upgraded(address indexed implementation);

    /**
     * @dev Returns the current implementation address.
     */
    function _getImplementation() internal view returns (address) {
        return StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value;
    }

    /**
     * @dev Stores a new address in the EIP1967 implementation slot.
     */
    function _setImplementation(address newImplementation) private {
        require(Address.isContract(newImplementation), "ERC1967: new implementation is not a contract");
        StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value = newImplementation;
    }

    /**
     * @dev Perform implementation upgrade
     *
     * Emits an {Upgraded} event.
     */
    function _upgradeTo(address newImplementation) internal {
        _setImplementation(newImplementation);
        emit Upgraded(newImplementation);
    }

    /**
     * @dev Perform implementation upgrade with additional setup call.
     *
     * Emits an {Upgraded} event.
     */
    function _upgradeToAndCall(
        address newImplementation,
        bytes memory data,
        bool forceCall
    ) internal {
        _upgradeTo(newImplementation);
        if (data.length > 0 || forceCall) {
            Address.functionDelegateCall(newImplementation, data);
        }
    }

    /**
     * @dev Perform implementation upgrade with security checks for UUPS proxies, and additional setup call.
     *
     * Emits an {Upgraded} event.
     */
    function _upgradeToAndCallUUPS(
        address newImplementation,
        bytes memory data,
        bool forceCall
    ) internal {
        // Upgrades from old implementations will perform a rollback test. This test requires the new
        // implementation to upgrade back to the old, non-ERC1822 compliant, implementation. Removing
        // this special case will break upgrade paths from old UUPS implementation to new ones.
        if (StorageSlot.getBooleanSlot(_ROLLBACK_SLOT).value) {
            _setImplementation(newImplementation);
        } else {
            try IERC1822Proxiable(newImplementation).proxiableUUID() returns (bytes32 slot) {
                require(slot == _IMPLEMENTATION_SLOT, "ERC1967Upgrade: unsupported proxiableUUID");
            } catch {
                revert("ERC1967Upgrade: new implementation is not UUPS");
            }
            _upgradeToAndCall(newImplementation, data, forceCall);
        }
    }

    /**
     * @dev Storage slot with the admin of the contract.
     * This is the keccak-256 hash of "eip1967.proxy.admin" subtracted by 1, and is
     * validated in the constructor.
     */
    bytes32 internal constant _ADMIN_SLOT = 0xb53127684a568b3173ae13b9f8a6016e243e63b6e8ee1178d6a717850b5d6103;

    /**
     * @dev Emitted when the admin account has changed.
     */
    event AdminChanged(address previousAdmin, address newAdmin);

    /**
     * @dev Returns the current admin.
     */
    function _getAdmin() internal view returns (address) {
        return StorageSlot.getAddressSlot(_ADMIN_SLOT).value;
    }

    /**
     * @dev Stores a new address in the EIP1967 admin slot.
     */
    function _setAdmin(address newAdmin) private {
        require(newAdmin != address(0), "ERC1967: new admin is the zero address");
        StorageSlot.getAddressSlot(_ADMIN_SLOT).value = newAdmin;
    }

    /**
     * @dev Changes the admin of the proxy.
     *
     * Emits an {AdminChanged} event.
     */
    function _changeAdmin(address newAdmin) internal {
        emit AdminChanged(_getAdmin(), newAdmin);
        _setAdmin(newAdmin);
    }

    /**
     * @dev The storage slot of the UpgradeableBeacon contract which defines the implementation for this proxy.
     * This is bytes32(uint256(keccak256('eip1967.proxy.beacon')) - 1)) and is validated in the constructor.
     */
    bytes32 internal constant _BEACON_SLOT = 0xa3f0ad74e5423aebfd80d3ef4346578335a9a72aeaee59ff6cb3582b35133d50;

    /**
     * @dev Emitted when the beacon is upgraded.
     */
    event BeaconUpgraded(address indexed beacon);

    /**
     * @dev Returns the current beacon.
     */
    function _getBeacon() internal view returns (address) {
        return StorageSlot.getAddressSlot(_BEACON_SLOT).value;
    }

    /**
     * @dev Stores a new beacon in the EIP1967 beacon slot.
     */
    function _setBeacon(address newBeacon) private {
        require(Address.isContract(newBeacon), "ERC1967: new beacon is not a contract");
        require(
            Address.isContract(IBeacon(newBeacon).implementation()),
            "ERC1967: beacon implementation is not a contract"
        );
        StorageSlot.getAddressSlot(_BEACON_SLOT).value = newBeacon;
    }

    /**
     * @dev Perform beacon upgrade with additional setup call. Note: This upgrades the address of the beacon, it does
     * not upgrade the implementation contained in the beacon (see {UpgradeableBeacon-_setImplementation} for that).
     *
     * Emits a {BeaconUpgraded} event.
     */
    function _upgradeBeaconToAndCall(
        address newBeacon,
        bytes memory data,
        bool forceCall
    ) internal {
        _setBeacon(newBeacon);
        emit BeaconUpgraded(newBeacon);
        if (data.length > 0 || forceCall) {
            Address.functionDelegateCall(IBeacon(newBeacon).implementation(), data);
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (proxy/utils/UUPSUpgradeable.sol)

pragma solidity ^0.8.0;

import "../../interfaces/draft-IERC1822.sol";
import "../ERC1967/ERC1967Upgrade.sol";

/**
 * @dev An upgradeability mechanism designed for UUPS proxies. The functions included here can perform an upgrade of an
 * {ERC1967Proxy}, when this contract is set as the implementation behind such a proxy.
 *
 * A security mechanism ensures that an upgrade does not turn off upgradeability accidentally, although this risk is
 * reinstated if the upgrade retains upgradeability but removes the security mechanism, e.g. by replacing
 * `UUPSUpgradeable` with a custom implementation of upgrades.
 *
 * The {_authorizeUpgrade} function must be overridden to include access restriction to the upgrade mechanism.
 *
 * _Available since v4.1._
 */
abstract contract UUPSUpgradeable is IERC1822Proxiable, ERC1967Upgrade {
    /// @custom:oz-upgrades-unsafe-allow state-variable-immutable state-variable-assignment
    address private immutable __self = address(this);

    /**
     * @dev Check that the execution is being performed through a delegatecall call and that the execution context is
     * a proxy contract with an implementation (as defined in ERC1967) pointing to self. This should only be the case
     * for UUPS and transparent proxies that are using the current contract as their implementation. Execution of a
     * function through ERC1167 minimal proxies (clones) would not normally pass this test, but is not guaranteed to
     * fail.
     */
    modifier onlyProxy() {
        require(address(this) != __self, "Function must be called through delegatecall");
        require(_getImplementation() == __self, "Function must be called through active proxy");
        _;
    }

    /**
     * @dev Check that the execution is not being performed through a delegate call. This allows a function to be
     * callable on the implementing contract but not through proxies.
     */
    modifier notDelegated() {
        require(address(this) == __self, "UUPSUpgradeable: must not be called through delegatecall");
        _;
    }

    /**
     * @dev Implementation of the ERC1822 {proxiableUUID} function. This returns the storage slot used by the
     * implementation. It is used to validate that the this implementation remains valid after an upgrade.
     *
     * IMPORTANT: A proxy pointing at a proxiable contract should not be considered proxiable itself, because this risks
     * bricking a proxy that upgrades to it, by delegating to itself until out of gas. Thus it is critical that this
     * function revert if invoked through a proxy. This is guaranteed by the `notDelegated` modifier.
     */
    function proxiableUUID() external view virtual override notDelegated returns (bytes32) {
        return _IMPLEMENTATION_SLOT;
    }

    /**
     * @dev Upgrade the implementation of the proxy to `newImplementation`.
     *
     * Calls {_authorizeUpgrade}.
     *
     * Emits an {Upgraded} event.
     */
    function upgradeTo(address newImplementation) external virtual onlyProxy {
        _authorizeUpgrade(newImplementation);
        _upgradeToAndCallUUPS(newImplementation, new bytes(0), false);
    }

    /**
     * @dev Upgrade the implementation of the proxy to `newImplementation`, and subsequently execute the function call
     * encoded in `data`.
     *
     * Calls {_authorizeUpgrade}.
     *
     * Emits an {Upgraded} event.
     */
    function upgradeToAndCall(address newImplementation, bytes memory data) external payable virtual onlyProxy {
        _authorizeUpgrade(newImplementation);
        _upgradeToAndCallUUPS(newImplementation, data, true);
    }

    /**
     * @dev Function that should revert when `msg.sender` is not authorized to upgrade the contract. Called by
     * {upgradeTo} and {upgradeToAndCall}.
     *
     * Normally, this function will use an xref:access.adoc[access control] modifier such as {Ownable-onlyOwner}.
     *
     * ```solidity
     * function _authorizeUpgrade(address) internal override onlyOwner {}
     * ```
     */
    function _authorizeUpgrade(address newImplementation) internal virtual;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC20/ERC20.sol)

pragma solidity ^0.8.0;

import "./IERC20.sol";
import "./extensions/IERC20Metadata.sol";
import "../../utils/Context.sol";

/**
 * @dev Implementation of the {IERC20} interface.
 *
 * This implementation is agnostic to the way tokens are created. This means
 * that a supply mechanism has to be added in a derived contract using {_mint}.
 * For a generic mechanism see {ERC20PresetMinterPauser}.
 *
 * TIP: For a detailed writeup see our guide
 * https://forum.zeppelin.solutions/t/how-to-implement-erc20-supply-mechanisms/226[How
 * to implement supply mechanisms].
 *
 * We have followed general OpenZeppelin Contracts guidelines: functions revert
 * instead returning `false` on failure. This behavior is nonetheless
 * conventional and does not conflict with the expectations of ERC20
 * applications.
 *
 * Additionally, an {Approval} event is emitted on calls to {transferFrom}.
 * This allows applications to reconstruct the allowance for all accounts just
 * by listening to said events. Other implementations of the EIP may not emit
 * these events, as it isn't required by the specification.
 *
 * Finally, the non-standard {decreaseAllowance} and {increaseAllowance}
 * functions have been added to mitigate the well-known issues around setting
 * allowances. See {IERC20-approve}.
 */
contract ERC20 is Context, IERC20, IERC20Metadata {
    mapping(address => uint256) private _balances;

    mapping(address => mapping(address => uint256)) private _allowances;

    uint256 private _totalSupply;

    string private _name;
    string private _symbol;

    /**
     * @dev Sets the values for {name} and {symbol}.
     *
     * The default value of {decimals} is 18. To select a different value for
     * {decimals} you should overload it.
     *
     * All two of these values are immutable: they can only be set once during
     * construction.
     */
    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev Returns the name of the token.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the symbol of the token, usually a shorter version of the
     * name.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the number of decimals used to get its user representation.
     * For example, if `decimals` equals `2`, a balance of `505` tokens should
     * be displayed to a user as `5.05` (`505 / 10 ** 2`).
     *
     * Tokens usually opt for a value of 18, imitating the relationship between
     * Ether and Wei. This is the value {ERC20} uses, unless this function is
     * overridden;
     *
     * NOTE: This information is only used for _display_ purposes: it in
     * no way affects any of the arithmetic of the contract, including
     * {IERC20-balanceOf} and {IERC20-transfer}.
     */
    function decimals() public view virtual override returns (uint8) {
        return 18;
    }

    /**
     * @dev See {IERC20-totalSupply}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        return _totalSupply;
    }

    /**
     * @dev See {IERC20-balanceOf}.
     */
    function balanceOf(address account) public view virtual override returns (uint256) {
        return _balances[account];
    }

    /**
     * @dev See {IERC20-transfer}.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - the caller must have a balance of at least `amount`.
     */
    function transfer(address to, uint256 amount) public virtual override returns (bool) {
        address owner = _msgSender();
        _transfer(owner, to, amount);
        return true;
    }

    /**
     * @dev See {IERC20-allowance}.
     */
    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        return _allowances[owner][spender];
    }

    /**
     * @dev See {IERC20-approve}.
     *
     * NOTE: If `amount` is the maximum `uint256`, the allowance is not updated on
     * `transferFrom`. This is semantically equivalent to an infinite approval.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function approve(address spender, uint256 amount) public virtual override returns (bool) {
        address owner = _msgSender();
        _approve(owner, spender, amount);
        return true;
    }

    /**
     * @dev See {IERC20-transferFrom}.
     *
     * Emits an {Approval} event indicating the updated allowance. This is not
     * required by the EIP. See the note at the beginning of {ERC20}.
     *
     * NOTE: Does not update the allowance if the current allowance
     * is the maximum `uint256`.
     *
     * Requirements:
     *
     * - `from` and `to` cannot be the zero address.
     * - `from` must have a balance of at least `amount`.
     * - the caller must have allowance for ``from``'s tokens of at least
     * `amount`.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) public virtual override returns (bool) {
        address spender = _msgSender();
        _spendAllowance(from, spender, amount);
        _transfer(from, to, amount);
        return true;
    }

    /**
     * @dev Atomically increases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        address owner = _msgSender();
        _approve(owner, spender, _allowances[owner][spender] + addedValue);
        return true;
    }

    /**
     * @dev Atomically decreases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `spender` must have allowance for the caller of at least
     * `subtractedValue`.
     */
    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        address owner = _msgSender();
        uint256 currentAllowance = _allowances[owner][spender];
        require(currentAllowance >= subtractedValue, "ERC20: decreased allowance below zero");
        unchecked {
            _approve(owner, spender, currentAllowance - subtractedValue);
        }

        return true;
    }

    /**
     * @dev Moves `amount` of tokens from `sender` to `recipient`.
     *
     * This internal function is equivalent to {transfer}, and can be used to
     * e.g. implement automatic token fees, slashing mechanisms, etc.
     *
     * Emits a {Transfer} event.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `from` must have a balance of at least `amount`.
     */
    function _transfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {
        require(from != address(0), "ERC20: transfer from the zero address");
        require(to != address(0), "ERC20: transfer to the zero address");

        _beforeTokenTransfer(from, to, amount);

        uint256 fromBalance = _balances[from];
        require(fromBalance >= amount, "ERC20: transfer amount exceeds balance");
        unchecked {
            _balances[from] = fromBalance - amount;
        }
        _balances[to] += amount;

        emit Transfer(from, to, amount);

        _afterTokenTransfer(from, to, amount);
    }

    /** @dev Creates `amount` tokens and assigns them to `account`, increasing
     * the total supply.
     *
     * Emits a {Transfer} event with `from` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function _mint(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: mint to the zero address");

        _beforeTokenTransfer(address(0), account, amount);

        _totalSupply += amount;
        _balances[account] += amount;
        emit Transfer(address(0), account, amount);

        _afterTokenTransfer(address(0), account, amount);
    }

    /**
     * @dev Destroys `amount` tokens from `account`, reducing the
     * total supply.
     *
     * Emits a {Transfer} event with `to` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     * - `account` must have at least `amount` tokens.
     */
    function _burn(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: burn from the zero address");

        _beforeTokenTransfer(account, address(0), amount);

        uint256 accountBalance = _balances[account];
        require(accountBalance >= amount, "ERC20: burn amount exceeds balance");
        unchecked {
            _balances[account] = accountBalance - amount;
        }
        _totalSupply -= amount;

        emit Transfer(account, address(0), amount);

        _afterTokenTransfer(account, address(0), amount);
    }

    /**
     * @dev Sets `amount` as the allowance of `spender` over the `owner` s tokens.
     *
     * This internal function is equivalent to `approve`, and can be used to
     * e.g. set automatic allowances for certain subsystems, etc.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `owner` cannot be the zero address.
     * - `spender` cannot be the zero address.
     */
    function _approve(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    /**
     * @dev Spend `amount` form the allowance of `owner` toward `spender`.
     *
     * Does not update the allowance amount in case of infinite allowance.
     * Revert if not enough allowance is available.
     *
     * Might emit an {Approval} event.
     */
    function _spendAllowance(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual {
        uint256 currentAllowance = allowance(owner, spender);
        if (currentAllowance != type(uint256).max) {
            require(currentAllowance >= amount, "ERC20: insufficient allowance");
            unchecked {
                _approve(owner, spender, currentAllowance - amount);
            }
        }
    }

    /**
     * @dev Hook that is called before any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * will be transferred to `to`.
     * - when `from` is zero, `amount` tokens will be minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens will be burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {}

    /**
     * @dev Hook that is called after any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * has been transferred to `to`.
     * - when `from` is zero, `amount` tokens have been minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens have been burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _afterTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {}
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/IERC20Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";
import "../../../utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (utils/cryptography/ECDSA.sol)

pragma solidity ^0.8.0;

import "../Strings.sol";

/**
 * @dev Elliptic Curve Digital Signature Algorithm (ECDSA) operations.
 *
 * These functions can be used to verify that a message was signed by the holder
 * of the private keys of a given address.
 */
library ECDSA {
    enum RecoverError {
        NoError,
        InvalidSignature,
        InvalidSignatureLength,
        InvalidSignatureS,
        InvalidSignatureV
    }

    function _throwError(RecoverError error) private pure {
        if (error == RecoverError.NoError) {
            return; // no error: do nothing
        } else if (error == RecoverError.InvalidSignature) {
            revert("ECDSA: invalid signature");
        } else if (error == RecoverError.InvalidSignatureLength) {
            revert("ECDSA: invalid signature length");
        } else if (error == RecoverError.InvalidSignatureS) {
            revert("ECDSA: invalid signature 's' value");
        } else if (error == RecoverError.InvalidSignatureV) {
            revert("ECDSA: invalid signature 'v' value");
        }
    }

    /**
     * @dev Returns the address that signed a hashed message (`hash`) with
     * `signature` or error string. This address can then be used for verification purposes.
     *
     * The `ecrecover` EVM opcode allows for malleable (non-unique) signatures:
     * this function rejects them by requiring the `s` value to be in the lower
     * half order, and the `v` value to be either 27 or 28.
     *
     * IMPORTANT: `hash` _must_ be the result of a hash operation for the
     * verification to be secure: it is possible to craft signatures that
     * recover to arbitrary addresses for non-hashed data. A safe way to ensure
     * this is by receiving a hash of the original message (which may otherwise
     * be too long), and then calling {toEthSignedMessageHash} on it.
     *
     * Documentation for signature generation:
     * - with https://web3js.readthedocs.io/en/v1.3.4/web3-eth-accounts.html#sign[Web3.js]
     * - with https://docs.ethers.io/v5/api/signer/#Signer-signMessage[ethers]
     *
     * _Available since v4.3._
     */
    function tryRecover(bytes32 hash, bytes memory signature) internal pure returns (address, RecoverError) {
        // Check the signature length
        // - case 65: r,s,v signature (standard)
        // - case 64: r,vs signature (cf https://eips.ethereum.org/EIPS/eip-2098) _Available since v4.1._
        if (signature.length == 65) {
            bytes32 r;
            bytes32 s;
            uint8 v;
            // ecrecover takes the signature parameters, and the only way to get them
            // currently is to use assembly.
            assembly {
                r := mload(add(signature, 0x20))
                s := mload(add(signature, 0x40))
                v := byte(0, mload(add(signature, 0x60)))
            }
            return tryRecover(hash, v, r, s);
        } else if (signature.length == 64) {
            bytes32 r;
            bytes32 vs;
            // ecrecover takes the signature parameters, and the only way to get them
            // currently is to use assembly.
            assembly {
                r := mload(add(signature, 0x20))
                vs := mload(add(signature, 0x40))
            }
            return tryRecover(hash, r, vs);
        } else {
            return (address(0), RecoverError.InvalidSignatureLength);
        }
    }

    /**
     * @dev Returns the address that signed a hashed message (`hash`) with
     * `signature`. This address can then be used for verification purposes.
     *
     * The `ecrecover` EVM opcode allows for malleable (non-unique) signatures:
     * this function rejects them by requiring the `s` value to be in the lower
     * half order, and the `v` value to be either 27 or 28.
     *
     * IMPORTANT: `hash` _must_ be the result of a hash operation for the
     * verification to be secure: it is possible to craft signatures that
     * recover to arbitrary addresses for non-hashed data. A safe way to ensure
     * this is by receiving a hash of the original message (which may otherwise
     * be too long), and then calling {toEthSignedMessageHash} on it.
     */
    function recover(bytes32 hash, bytes memory signature) internal pure returns (address) {
        (address recovered, RecoverError error) = tryRecover(hash, signature);
        _throwError(error);
        return recovered;
    }

    /**
     * @dev Overload of {ECDSA-tryRecover} that receives the `r` and `vs` short-signature fields separately.
     *
     * See https://eips.ethereum.org/EIPS/eip-2098[EIP-2098 short signatures]
     *
     * _Available since v4.3._
     */
    function tryRecover(
        bytes32 hash,
        bytes32 r,
        bytes32 vs
    ) internal pure returns (address, RecoverError) {
        bytes32 s = vs & bytes32(0x7fffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff);
        uint8 v = uint8((uint256(vs) >> 255) + 27);
        return tryRecover(hash, v, r, s);
    }

    /**
     * @dev Overload of {ECDSA-recover} that receives the `r and `vs` short-signature fields separately.
     *
     * _Available since v4.2._
     */
    function recover(
        bytes32 hash,
        bytes32 r,
        bytes32 vs
    ) internal pure returns (address) {
        (address recovered, RecoverError error) = tryRecover(hash, r, vs);
        _throwError(error);
        return recovered;
    }

    /**
     * @dev Overload of {ECDSA-tryRecover} that receives the `v`,
     * `r` and `s` signature fields separately.
     *
     * _Available since v4.3._
     */
    function tryRecover(
        bytes32 hash,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) internal pure returns (address, RecoverError) {
        // EIP-2 still allows signature malleability for ecrecover(). Remove this possibility and make the signature
        // unique. Appendix F in the Ethereum Yellow paper (https://ethereum.github.io/yellowpaper/paper.pdf), defines
        // the valid range for s in (301): 0 < s < secp256k1n  2 + 1, and for v in (302): v  {27, 28}. Most
        // signatures from current libraries generate a unique signature with an s-value in the lower half order.
        //
        // If your library generates malleable signatures, such as s-values in the upper range, calculate a new s-value
        // with 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEBAAEDCE6AF48A03BBFD25E8CD0364141 - s1 and flip v from 27 to 28 or
        // vice versa. If your library also generates signatures with 0/1 for v instead 27/28, add 27 to v to accept
        // these malleable signatures as well.
        if (uint256(s) > 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5D576E7357A4501DDFE92F46681B20A0) {
            return (address(0), RecoverError.InvalidSignatureS);
        }
        if (v != 27 && v != 28) {
            return (address(0), RecoverError.InvalidSignatureV);
        }

        // If the signature is valid (and not malleable), return the signer address
        address signer = ecrecover(hash, v, r, s);
        if (signer == address(0)) {
            return (address(0), RecoverError.InvalidSignature);
        }

        return (signer, RecoverError.NoError);
    }

    /**
     * @dev Overload of {ECDSA-recover} that receives the `v`,
     * `r` and `s` signature fields separately.
     */
    function recover(
        bytes32 hash,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) internal pure returns (address) {
        (address recovered, RecoverError error) = tryRecover(hash, v, r, s);
        _throwError(error);
        return recovered;
    }

    /**
     * @dev Returns an Ethereum Signed Message, created from a `hash`. This
     * produces hash corresponding to the one signed with the
     * https://eth.wiki/json-rpc/API#eth_sign[`eth_sign`]
     * JSON-RPC method as part of EIP-191.
     *
     * See {recover}.
     */
    function toEthSignedMessageHash(bytes32 hash) internal pure returns (bytes32) {
        // 32 is the length in bytes of hash,
        // enforced by the type signature above
        return keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", hash));
    }

    /**
     * @dev Returns an Ethereum Signed Message, created from `s`. This
     * produces hash corresponding to the one signed with the
     * https://eth.wiki/json-rpc/API#eth_sign[`eth_sign`]
     * JSON-RPC method as part of EIP-191.
     *
     * See {recover}.
     */
    function toEthSignedMessageHash(bytes memory s) internal pure returns (bytes32) {
        return keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n", Strings.toString(s.length), s));
    }

    /**
     * @dev Returns an Ethereum Signed Typed Data, created from a
     * `domainSeparator` and a `structHash`. This produces hash corresponding
     * to the one signed with the
     * https://eips.ethereum.org/EIPS/eip-712[`eth_signTypedData`]
     * JSON-RPC method as part of EIP-712.
     *
     * See {recover}.
     */
    function toTypedDataHash(bytes32 domainSeparator, bytes32 structHash) internal pure returns (bytes32) {
        return keccak256(abi.encodePacked("\x19\x01", domainSeparator, structHash));
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/StorageSlot.sol)

pragma solidity ^0.8.0;

/**
 * @dev Library for reading and writing primitive types to specific storage slots.
 *
 * Storage slots are often used to avoid storage conflict when dealing with upgradeable contracts.
 * This library helps with reading and writing to such slots without the need for inline assembly.
 *
 * The functions in this library return Slot structs that contain a `value` member that can be used to read or write.
 *
 * Example usage to set ERC1967 implementation slot:
 * ```
 * contract ERC1967 {
 *     bytes32 internal constant _IMPLEMENTATION_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;
 *
 *     function _getImplementation() internal view returns (address) {
 *         return StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value;
 *     }
 *
 *     function _setImplementation(address newImplementation) internal {
 *         require(Address.isContract(newImplementation), "ERC1967: new implementation is not a contract");
 *         StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value = newImplementation;
 *     }
 * }
 * ```
 *
 * _Available since v4.1 for `address`, `bool`, `bytes32`, and `uint256`._
 */
library StorageSlot {
    struct AddressSlot {
        address value;
    }

    struct BooleanSlot {
        bool value;
    }

    struct Bytes32Slot {
        bytes32 value;
    }

    struct Uint256Slot {
        uint256 value;
    }

    /**
     * @dev Returns an `AddressSlot` with member `value` located at `slot`.
     */
    function getAddressSlot(bytes32 slot) internal pure returns (AddressSlot storage r) {
        assembly {
            r.slot := slot
        }
    }

    /**
     * @dev Returns an `BooleanSlot` with member `value` located at `slot`.
     */
    function getBooleanSlot(bytes32 slot) internal pure returns (BooleanSlot storage r) {
        assembly {
            r.slot := slot
        }
    }

    /**
     * @dev Returns an `Bytes32Slot` with member `value` located at `slot`.
     */
    function getBytes32Slot(bytes32 slot) internal pure returns (Bytes32Slot storage r) {
        assembly {
            r.slot := slot
        }
    }

    /**
     * @dev Returns an `Uint256Slot` with member `value` located at `slot`.
     */
    function getUint256Slot(bytes32 slot) internal pure returns (Uint256Slot storage r) {
        assembly {
            r.slot := slot
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Strings.sol)

pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }
}

pragma solidity 0.8.10;


library AspisLibrary {

    uint256 private constant SECONDS_IN_YEAR = 365 * 24 * 60 * 60;

    function calculateProRataShare(
        uint256 _balance,
        uint256 _amount,
        uint256 _totalSupply
    ) internal pure returns (uint256) {
        return (_amount * _balance) / _totalSupply; //20, 8, 10 = (8 * 20 / 10) = 16
    }

    function calculatePerformanceFee(uint256 _currentPrice, uint256 _averagePrice, uint256 _withdrawAmount, uint256 _totalSupply, uint256 _poolValueUSD, uint256 _performanceFee) internal pure returns(uint256) {
        if (_currentPrice > _averagePrice) { 
                uint256 _a = (_currentPrice - _averagePrice) * _withdrawAmount * _performanceFee; // (2000 - 1000) * (20 * 10^18) * 2000 = 4e25
                uint256 _numerator = _totalSupply * _a; // (2 * 10^18) * 4e25 = 0.8
                uint256 _denominator = (_poolValueUSD * 1e22) - _a; // (10 ** usd_decimal_places * 10 ** fee_decimal_places * 100(for percentage))
                //(40000) * (10 ** 22) - 4e25 = 3.6
                return  _numerator / _denominator;  // 0.8 / 3.6 = 0.22222
        }

        return 0;
    }

    function calculateFundManagementFee(uint256 _currentTimestamp, uint _lastFundManagementFeeTimestamp, uint256 _totalSupply, uint256 _fundManagementFee) internal pure returns(uint256) {
        uint256 _a = _fundManagementFee * (_currentTimestamp - _lastFundManagementFeeTimestamp);
        uint256 _numerator = _totalSupply * _a;
        uint256 _denominator = (SECONDS_IN_YEAR * 10**4) - _a;
        return _numerator / _denominator;
    }

    /**
    * @notice returns true if withdraws are within the withdraw period or there is no withdraw and freeze period
    */
    function isWithdrawalWithinWindow(uint256 _withdrawPeriod, uint256 _freezePeriod, uint256 _fundraisingFinishTime) internal view returns(bool) {
        
        if((_withdrawPeriod + _freezePeriod) == 0) {
            return true;
        }

        uint256 _currentTime = block.timestamp;

        //counting seconds passed after the fundraising period is over as freeze and withdraw windows start after it.
        uint256 _countPastSeconds = (_currentTime - _fundraisingFinishTime);

        //taking mod over the total past seconds between current time and fundraising finish time
        uint256 _currentRelativeDay = _countPastSeconds % (_withdrawPeriod + _freezePeriod);


        if (_currentRelativeDay >= _freezePeriod) {
            return true;
        }
        return false;
    }

    function isWithdrawalWithinFundraising(uint256 _fundraisingFinishTime) internal view returns(bool) {
        uint256 _currentTime = block.timestamp;
        //before fundraising is over
        if (_fundraisingFinishTime > _currentTime) {
            return true;  //rage quit will apply
        }

        return false;
    }

}

pragma solidity 0.8.10;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "@openzeppelin/contracts/proxy/utils/UUPSUpgradeable.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/utils/Address.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "../core/acl/ACL.sol";
import "./ITokenValueCalculator.sol";
import "./IAspisPool.sol";
import "./IAspisGovernanceERC20.sol";
import "./IAspisConfiguration.sol";
import "./AspisProposal.sol";
import "./AspisLibrary.sol";
import "../registry/IAspisRegistry.sol";
import "./IAspisDecoder.sol";
import "../external/Permit2Lib.sol";
import "../external/ECDSAExternal.sol";

contract AspisPool is
    IAspisPool,
    Initializable,
    UUPSUpgradeable,
    ACL,
    AspisProposal,
    ReentrancyGuard
{
    using SafeERC20 for IERC20;
    using Address for address;

    uint256 internal constant SUPPORTED_USD_DECIMALS = 4;

    uint256 internal constant SUPPORTED_TIME_UNIT = 1 days;

    // Roles
    bytes32 public constant UPGRADE_ROLE = keccak256("UPGRADE_ROLE");
    bytes32 public constant DAO_CONFIG_ROLE = keccak256("DAO_CONFIG_ROLE");
    bytes32 public constant EXEC_ROLE = keccak256("EXEC_ROLE");

    uint256 internal constant SLIPPAGE_TOLERANCE_PERCENTAGE = 500;
    address internal constant ETH = 0xEeeeeEeeeEeEeeEeEeEeeEEEeeeeEeeeeeeeEEeE;

    bytes4 private constant EIP1271_MAGIC_VALUE = 0x1626ba7e;

    bool public emergencyStopActivated;
    
    ITokenValueCalculator private calculator;
    IAspisGovernanceERC20 private token;
    IAspisRegistry private registry;
    address private guardian;
    IAspisConfiguration public configuration;

    //to know if there has been at least one deposit after DAO creation
    bool internal hasUsedDeposit;

    struct Deposit {
        uint256 price;
        uint256 amount;
    }

    address internal manager;
    uint256 public managerBalance;

    uint256 private lastFundManagementFeeTimestamp;

    mapping(address => uint256) private lockedUntil;
    mapping(address => Deposit[]) private depositsOfUser;
    mapping(address => uint256) private worthOfUserAsset; 

    // Error msg's
    /// @notice Thrown if action execution has failed
    error ActionFailed();

    /// @notice Thrown if the deposit or withdraw amount is zero
    error ZeroAmount();

    /// @notice Thrown if the expected and actually deposited ETH amount mismatch
    /// @param expected ETH amount
    /// @param actual ETH amount
    error ETHDepositAmountMismatch(uint256 expected, uint256 actual);

    /// @notice Thrown if an ETH withdraw fails
    error ETHWithdrawFailed();
    error EmergencyMode();
    error NotManager();
    error NotGuardian();
    error UnsupportedProtocol();
    error UnsupportedToken();
    error InvalidSignature(uint8 code);
    error TradeExceededSlippageTolerance();
    error FundraisingOverOrNotStarted();
    error FundraisingInProgress();
    error UserNotWhitelisted();
    error DepositLimitError();
    error AssetsLocked();

    modifier notEmergencyMode() {
        if (emergencyStopActivated) revert EmergencyMode();
        _;
    }

    modifier onlyManager() {
        if (msg.sender != manager) revert NotManager();
        _;
    }

    modifier trustedProtocol(address protocol) {
        if (!configuration.supportsProtocol(protocol)) revert UnsupportedProtocol();
        _;
    }

    function initialize(
        address[7] calldata _configurationAddresses
    ) external initializer {
        calculator = ITokenValueCalculator(_configurationAddresses[2]);
        token = IAspisGovernanceERC20(_configurationAddresses[1]);
        configuration = IAspisConfiguration(_configurationAddresses[3]);
        registry = IAspisRegistry(_configurationAddresses[4]);
        manager = _configurationAddresses[5];
        guardian = _configurationAddresses[6];
        __ACL_init(_configurationAddresses[0]);

    }

    /// @dev Used to check the permissions within the upgradability pattern implementation of OZ
    function _authorizeUpgrade(address) internal virtual override auth(address(this), UPGRADE_ROLE) {}

    /// @notice Checks if the current callee has the permissions for.
    /// @dev Wrapper for the willPerform method of ACL to later on be able to use it in the modifier of the sub components of this DAO.
    /// @param _where Which contract does get called
    /// @param _who Who is calling this method
    /// @param _role Which role is required to call this
    /// @param _data Additional data used in the ACLOracle
    function hasPermission(
        address _where,
        address _who,
        bytes32 _role,
        bytes memory _data
    ) external override returns (bool) {
        return willPerform(_where, _who, _role, _data);
    }
    

    function updateManager(address _manager) external auth(address(this), DAO_CONFIG_ROLE) {
        manager = _manager;
    }


    function deposit(address _token, uint256 _amount) external payable override nonReentrant notEmergencyMode {
        if (!configuration.supportsDepositToken(_token)) {
            revert UnsupportedToken();
        }
        
        if (_amount == 0) revert ZeroAmount();

        if (!configuration.isPublicFund() && !configuration.userWhitelisted(msg.sender)) {
            revert UserNotWhitelisted();
        }

        if (block.timestamp < configuration.startTime() || configuration.finishTime() < block.timestamp) {
            revert FundraisingOverOrNotStarted();
        }

        uint256 _depositValue = calculator.convert(_token, _amount);
        validateDepositLimit(_depositValue, msg.sender);

        lockedUntil[msg.sender] = block.timestamp + (1 hours * configuration.lockLimit());

        uint256 _fundManagementFee = hasUsedDeposit ? fundManagementFee() : 0;
        managerBalance += _fundManagementFee;

        (uint256 _price, ) = getCurrentTokenPrice(_token == ETH && msg.value != 0 ? _amount : 0);
        
        lastFundManagementFeeTimestamp = block.timestamp;

        if (!hasUsedDeposit) {
            hasUsedDeposit = true;
        }

        if (_token == address(ETH)) {
            if (msg.value != _amount) revert ETHDepositAmountMismatch({expected: _amount, actual: msg.value});
        } else {
            if (msg.value != 0) revert ETHDepositAmountMismatch({expected: 0, actual: msg.value});

            IERC20 depositToken = IERC20(_token);
            uint256 balBefore = depositToken.balanceOf(address(this));
            depositToken.safeTransferFrom(msg.sender, address(this), _amount);
            uint256 balAfter = depositToken.balanceOf(address(this));

            require(balAfter - balBefore >= _amount, "Error");
        }

        uint256 _mintTokens = (_depositValue * (10**token.decimals())) / (_price);
        uint256 _entranceFee = (_mintTokens * configuration.entranceFee()) / (10000);

        worthOfUserAsset[msg.sender] += _depositValue;

        token.mint(msg.sender, _mintTokens - _entranceFee);
        managerBalance += _entranceFee;

        depositsOfUser[msg.sender].push(Deposit(_price, _mintTokens - _entranceFee));

        emit Deposited(msg.sender, _token, _amount, _mintTokens, _entranceFee, _fundManagementFee);

    }

    function withdraw(
        address _to
    ) external override nonReentrant {

        uint256 _amount = token.balanceOf(msg.sender);

        if (_amount == 0) revert ZeroAmount();

        if (lockedUntil[msg.sender] >= block.timestamp) revert AssetsLocked();

        uint256 _fundManagementFee = fundManagementFee();

        managerBalance += _fundManagementFee;

        (uint256 _currentLPTokenPrice, uint256 _poolValue) = getCurrentTokenPrice(0);
        
        lastFundManagementFeeTimestamp = block.timestamp;

        uint256 _rageQuitFee = isRageQuitFeeRequired() ? (_amount * configuration.rageQuitFee()) / 10000 : 0;

        Deposit[] memory _deposits = depositsOfUser[msg.sender];

        uint256 _totalSupply = getTokenSupply();

        uint256 _weightedAveragePrice = 0;

        uint256 i = _deposits.length;
        for (i; i > 0; ) {
            unchecked { --i; }
            _weightedAveragePrice += (_deposits[i].amount * _deposits[i].price) / _amount;
            depositsOfUser[msg.sender].pop();
        }

        uint256 _performanceFee = AspisLibrary.calculatePerformanceFee(_currentLPTokenPrice, _weightedAveragePrice, _amount, _totalSupply, _poolValue, configuration.performanceFee());

        worthOfUserAsset[msg.sender] = 0;

        token.burn(msg.sender, _amount); 
        //burning with rage quit fee
        managerBalance += _performanceFee; 

        transferAsset(registry.getAspisSupportedTradingTokens(), _to, _amount - _rageQuitFee, _totalSupply + _performanceFee); // 8, 10

        emit Withdrawn(address(0), _to, _amount, _rageQuitFee, _fundManagementFee, _performanceFee);
    }

    function withdrawCommission() external nonReentrant onlyManager {
        uint256 _amount = managerBalance;
        
        uint256 _LPTokenSupply = getTokenSupply();
        
        managerBalance = 0;

        transferAsset(registry.getAspisSupportedTradingTokens(), msg.sender, _amount, _LPTokenSupply);

    }

    /// @notice If called, the list of provided actions will be executed.
    /// @dev It run a loop through the array of acctions and execute one by one.
    /// @dev If one acction fails, all will be reverted.
    /// @param _actions The aray of actions
    function execute(uint256 callId, Action[] memory _actions)
        external
        override
        auth(address(this), EXEC_ROLE)
        notEmergencyMode
        returns (bytes[] memory)
    {
        bytes[] memory execResults = new bytes[](_actions.length);

        for (uint256 i = 0; i < _actions.length; i++) {
            (bool success, bytes memory response) = _actions[i].to.call{value: _actions[i].value}(_actions[i].data);

            if (!success) revert ActionFailed();

            execResults[i] = response;
        }

        emit Executed(msg.sender, callId, _actions, execResults);

        return execResults;
    }

    function approveTokenTransfer(address _token, address _spender, uint256 _amount) external onlyManager trustedProtocol(_spender) {
        IERC20(_token).safeApprove(_spender, _amount);

    }

    function execute(
        address _target,
        uint256 _ethValue,
        bytes calldata _data // This function MUST always be external as the function performs a low level return, exiting the Agent app execution context
    ) external notEmergencyMode onlyManager trustedProtocol(_target) {
        decodeAndCall(_target, _ethValue, _data);

    }

    function directAssetTransfer(
        address _target,
        uint256 _ethValue,
        bytes calldata _data // This function MUST always be external as the function performs a low level return, exiting the Agent app execution context
    ) external notEmergencyMode {
        if (block.timestamp <= configuration.finishTime()) revert FundraisingInProgress();
        require(msg.sender == address(this) && configuration.canPerformDirectTransfer(), "Unauthorized call");
    
        if (!registry.isAspisSupportedTradingToken(_target)) {
            revert UnsupportedToken();
        }
        
        executeLowLevelCall(_target, _ethValue, _data);
    }

    function emergencyStop() external {
        if (msg.sender != guardian) revert NotGuardian();

        emergencyStopActivated = true;

        IAspisConfiguration(configuration).setRageQuitFee(0);

    }

    function decodeAndCall( 
        address _target,
        uint256 _ethValue,
        bytes calldata _data) internal {

        address _decoder = registry.getDecoder(_target);

        if(_decoder == address(0)) {
            revert("Decoder not supported yet");
        }

        (address srcToken, address desToken, , ,) = IAspisDecoder(_decoder).decodeExchangeInput(_data);        

        if (!configuration.supportsTradingToken(desToken)) {
            revert UnsupportedToken();
        }

        uint256 _srcTokenAmountBefore = getBalance(srcToken);
        uint256 _desTokenAmountBefore = getBalance(desToken);

        executeLowLevelCall(_target, _ethValue, _data);

        uint256 _srcTokenAmountAfter = getBalance(srcToken);
        uint256 _desTokenAmountAfter = getBalance(desToken);

        meetsSlippageTolerance(srcToken, desToken, _srcTokenAmountBefore - _srcTokenAmountAfter, _desTokenAmountAfter - _desTokenAmountBefore);

    }


    function executeLowLevelCall(
        address _target,
        uint256 _ethValue,
        bytes calldata _data
    ) internal {
        
        (bool result, ) = _target.call{value: _ethValue}(_data);

        assembly {
            let ptr := mload(0x40)
            returndatacopy(ptr, 0, returndatasize())

            // revert instead of invalid() bc if the underlying call failed with invalid() it already wasted gas.
            // if the call returned error data, forward it
            switch result
            case 0 {
                revert(ptr, returndatasize())
            }
        }
    }

    /// @dev ERC1271 implementation. We accept signatures for Permit2. However, in order to validate that spender is a trusted protocol we extend the signature
    /// to include permit data. Signature has the following format ("address", "uint160", "uint48", "uint48", "address", "uint256", "bytes")
    /// where last parameter is an actual message signed by the manager and others are permit data
    function isValidSignature(bytes32 _hash, bytes calldata _signature) external view returns (bytes4 magicValue) {
        (address _token, uint160 amount, uint48 expiration, uint48 nonce, address spender, uint256 deadline, bytes memory signature) =
            abi.decode(_signature, (address, uint160, uint48, uint48, address, uint256, bytes));

        if (!configuration.supportsProtocol(spender)) {
            revert InvalidSignature(1);
        }

        bytes32 permitHash = Permit2Lib.hashData(_token, amount, expiration, nonce, spender, deadline);
        if (permitHash != _hash) revert InvalidSignature(2);

        address signer = ECDSAExternal.recover(_hash, signature);
        if (signer != manager) revert InvalidSignature(3);

        return EIP1271_MAGIC_VALUE;
    }

    function validateProposal(bytes calldata _proposal, address _creator) public override view returns(bool) {

        if(emergencyStopActivated) {
            return false;
        }

        bytes4 selector = bytes4(_proposal[:4]);

        if(selector == PROPOSAL_BURN || selector == PROPOSAL_MINT) {
            return false;
        }
        
        return (
            (selector == PROPOSAL_UPDATE_MANAGER && configuration.canChangeManager())
            || selector == PROPOSAL_REMOVE_PROTOCOLS
            || selector == PROPOSAL_REMOVE_TRADING_TOKENS
            || _creator == manager
        );
    }

    function transferAsset(
        address[] memory _tokens,
        address _receiver,
        uint256 _amount,
        uint256 _tokenSupply
    ) internal {
        for (uint8 i = 0; i < _tokens.length; i++) {
            if (_tokens[i] == address(ETH)) {
                (bool ok, ) = _receiver.call{value: AspisLibrary.calculateProRataShare(getBalance(_tokens[i]), _amount, _tokenSupply)}("");
                if (!ok) revert ETHWithdrawFailed();
            } else {
                if (IERC20(_tokens[i]).balanceOf(address(this)) != 0) {
                    IERC20(_tokens[i]).safeTransfer(_receiver, AspisLibrary.calculateProRataShare(getBalance(_tokens[i]), _amount, _tokenSupply));
                }
            }
        }
    }

    function meetsSlippageTolerance(address srcToken,address desToken,uint256 inputAmount,uint256 outputAmount) internal {
        uint256 _srcTokenValue = calculator.convert(srcToken, inputAmount);
        uint256 _destTokenValue = calculator.convert(desToken, outputAmount);

        if(_srcTokenValue > _destTokenValue) {
            uint256 _slippage = ((_srcTokenValue - _destTokenValue) * 10000)/_srcTokenValue;

            if(_slippage > SLIPPAGE_TOLERANCE_PERCENTAGE) {
                revert TradeExceededSlippageTolerance();
            }
        }
    }


    /** 
    * @notice returns the current price of LP tokens of the DAO along with the USD pooled value of assets stored
    */
    function getCurrentTokenPrice(uint256 _tempETHBalance) internal returns (uint256 _price, uint256 _poolValue) {
        if (!hasUsedDeposit) {
            _price = configuration.initialPrice();
            return (_price, 0);
        } else {

            address[] memory _tokens = registry.getAspisSupportedTradingTokens();

            for (uint64 i = 0; i < _tokens.length; i++) {
                uint256 _balance = _tokens[i] == ETH
                    ? (address(this).balance) - _tempETHBalance
                    : IERC20(_tokens[i]).balanceOf(address(this));
                _poolValue += calculator.convert(_tokens[i], _balance);
            }

            // If pool value is 0 return DAO to initial state
            if(_poolValue <= 0) {
                return (configuration.initialPrice(), 0);
            }
            _price = (_poolValue * (10**token.decimals())) / getTokenSupply(); //1200/1000 = 1.2
            return (_price, _poolValue);
        }
    }

    /** 
    * @notice returns true if rage quit fee needs to be applied
    */
    function isRageQuitFeeRequired() internal view returns (bool) {

        uint256 _fundraisingFinishTime = configuration.finishTime();

        //rage quit fee applied if withdrawl within fund raising period
        if(AspisLibrary.isWithdrawalWithinFundraising(_fundraisingFinishTime)) {
            return true;
        }

        //rage quit fee applied if withdrawl outside of withdraw window
        if(!AspisLibrary.isWithdrawalWithinWindow(configuration.withdrawlWindow() * SUPPORTED_TIME_UNIT,  configuration.freezePeriod() * SUPPORTED_TIME_UNIT, _fundraisingFinishTime)) {
            return true;
        }

        return false;
    }

    function getTokenSupply() internal view returns (uint256) {
        return managerBalance + token.totalSupply();
    }

    function getBalance(address _token) internal view returns(uint256) {
        if(_token == ETH) {
            return address(this).balance;
        } else {
            return IERC20(_token).balanceOf(address(this));
        }
    }

    function validateDepositLimit(uint256 _depositValue, address _depositor) internal view {
        
        uint256 _currentDepositValue = _depositValue + worthOfUserAsset[_depositor];

        (uint256 _minDepositLimit, uint256 _maxDepositLimit) = configuration.getDepositLimit();
        
        if(_minDepositLimit > 0 && (_depositValue / (10**SUPPORTED_USD_DECIMALS)) < _minDepositLimit) {
            revert DepositLimitError();
        } 
        
        if(_maxDepositLimit != type(uint256).max && (_currentDepositValue / (10**SUPPORTED_USD_DECIMALS)) > _maxDepositLimit) {
            revert DepositLimitError();
        }
    }

    function getManager() public view override returns(address) {
        return manager;
    }

    function fundManagementFee() internal view returns(uint256) {
        return AspisLibrary.calculateFundManagementFee(block.timestamp, lastFundManagementFeeTimestamp, getTokenSupply(), configuration.fundManagementFee());
    }

}

pragma solidity 0.8.10;


abstract contract AspisProposal {
    // bytes4 constant internal PROPOSAL_CHANGE_FUNDRAISING_TARGET = 0x97edceef;
    // bytes4 constant internal PROPOSAL_CHANGE_FUNDRAISING_START_TIME = 0x5b41dfee;
    // bytes4 constant internal PROPOSAL_CHANGE_FUNDRAISING_FINISH_TIME = 0x11bdc357;
    // bytes4 constant internal PROPOSAL_CHANGE_VOTING_CONFIG = 0xbecbd871;
    // bytes4 constant internal PROPOSAL_CHANGE_ABILITY_TO_CHANGE_MANAGER = 0x44548363;
    bytes4 constant internal PROPOSAL_UPDATE_MANAGER = 0x58aba00f;
    // bytes4 constant internal PROPOSAL_ADD_SUPPORTED_TOKENS = 0x8c7ac746;
    // bytes4 constant internal PROPOSAL_REMOVE_SUPPORTED_TOKENS = 0x8a448c59;
    // bytes4 constant internal PROPOSAL_ADD_WALLETS = 0x7f649783;
    // bytes4 constant internal PROPOSAL_REMOVE_WALLETS = 0x548db174;
    // bytes4 constant internal PROPOSAL_ADD_PROTOCOLS = 0xac76a31c;
    bytes4 constant internal PROPOSAL_REMOVE_PROTOCOLS = 0x89ca0027;
    // bytes4 constant internal PROPOSAL_CHANGE_INITIAL_TOKEN_PRICE = 0x3f55306f;
    // bytes4 constant internal PROPOSAL_CHANGE_DEPOSIT_LIMITS = 0xd91cd644;
    // bytes4 constant internal PROPOSAL_CHANGE_WITHDRAWL_WINDOWS = 0x7b7b2105;
    // bytes4 constant internal PROPOSAL_CHANGE_LOCKUP_PERIOD = 0xa32bdee9;
    // bytes4 constant internal PROPOSAL_CHANGE_RAGE_QUIT_FEE = 0xb272a7e9;
    // bytes4 constant internal PROPOSAL_CHANGE_FUND_MANAGEMENT_FEE = 0x183bb2c1;
    // bytes4 constant internal PROPOSAL_CHANGE_PERFORMANCE_FEE = 0x70897b23;
    // bytes4 constant internal PROPOSAL_CHANGE_ENTRANCE_FEE = 0xfe56f5a0;
    // bytes4 constant internal PROPOSAL_DIRECT_ASSET_TRANSFER = 0x21feab07;
    // bytes4 constant internal PROPOSAL_ADD_DEFI_PROTOCOL = 0x30fb7402;
    // bytes4 constant internal PROPOSAL_REMOVE_DEFI_PROTOCOL = 0x520aa6fa;
    bytes4 constant internal PROPOSAL_MINT= 0x40c10f19;
    bytes4 constant internal PROPOSAL_BURN= 0x9dc29fac;
    bytes4 constant internal PROPOSAL_REMOVE_TRADING_TOKENS = 0xe8efe397;
}

pragma solidity 0.8.10;

abstract contract IAspisConfiguration {
    uint256 internal constant maxFeePercentage = 1e4;

    uint256 public entranceFee;
    uint256 public performanceFee;
    uint256 public fundManagementFee;
    uint256 public rageQuitFee;
    
    uint256 public maxCap; //fundraising limit
    uint256 public minDeposit; 
    uint256 public maxDeposit;
    uint256 public startTime; //fundraising start time
    uint256 public finishTime;  //fundraising end time
    uint256 public withdrawlWindow;
    uint256 public freezePeriod;
    uint256 public lockLimit; //token lock up period
    uint256 public spendingLimit;
    uint256 public initialPrice;
    bool public canChangeManager;
    bool public canPerformDirectTransfer;

    function setConfiguration(address _aspisPool,
        address _registry,
        uint256[16] memory _poolconfig,
        address[] calldata _whitelistUsers,
        address[] calldata _trustedProtocols,
        address[] calldata _supportedTokens,
        address[] calldata _tradingTokens
    ) external virtual;

    function setRageQuitFee(uint256) external virtual;
    
    function getDepositLimit() public view virtual returns(uint256, uint256);

    function isPublicFund() public view virtual returns(bool);

    function getWhiteListUsers() public view virtual returns(address[] memory);

    function getTradingTokens() view public virtual returns(address[] memory);

    function getTrustedProtocols() view public virtual returns(address[] memory);

    function getDepositTokens() view public virtual returns(address[] memory);

    function supportsProtocol(address) view public virtual returns (bool);
    
    function supportsTradingToken(address) view public virtual returns (bool);
    
    function supportsDepositToken(address) view public virtual returns (bool);

    function userWhitelisted(address) view public virtual returns (bool);
    
    function getFees() public view returns(uint256, uint256, uint256, uint256) {
        return (entranceFee, performanceFee, fundManagementFee, rageQuitFee);
    }

    function calculateFundManagementFee(uint256 _tokenSupply, uint256 _managerShare) public view returns (uint256) {
        return (fundManagementFee * _tokenSupply) / (365 * (10000 - _managerShare) - fundManagementFee);
    }

}

pragma solidity 0.8.10;

interface IAspisDecoder {
    function decodeExchangeInput(bytes calldata inputData) external returns(address, address, uint256, uint256, address);
}

pragma solidity 0.8.10;

interface IAspisGovernanceERC20 {

    function decimals() external view returns(uint8);

    function mint(address to, uint256 amount) external;

    function burn(address from, uint256 amount) external;
   
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

}

pragma solidity 0.8.10;

interface IExchangeDecoderRegistry {

    function getDecoder(address) external returns(address);
}

abstract contract IAspisPool {

    struct Action {
        address to; // Address to call.
        uint256 value; // Value to be sent with the call. for example (ETH)
        bytes data; // FuncSig + arguments
    }

    receive() external payable{
        //lets contract receive eth funds
    }

    /// @dev Required to handle the permissions within the whole DAO framework accordingly
    /// @param _where The address of the contract
    /// @param _who The address of a EOA or contract to give the permissions
    /// @param _role The hash of the role identifier
    /// @param _data The optional data passed to the ACLOracle registered.
    /// @return bool
    function hasPermission(
        address _where,
        address _who,
        bytes32 _role,
        bytes memory _data
    ) external virtual returns (bool);
    /// @notice If called, the list of provided actions will be executed.
    /// @dev It run a loop through the array of acctions and execute one by one.
    /// @dev If one acction fails, all will be reverted.
    /// @param _actions The aray of actions
    function execute(uint256 callId, Action[] memory _actions) external virtual returns (bytes[] memory);

    event Executed(address indexed actor, uint256 callId, Action[] actions, bytes[] execResults);

    /// @notice Deposit ETH or any token to this contract with a reference string
    /// @dev Deposit ETH (token address == 0) or any token with a reference
    /// @param _token The address of the token and in case of ETH address(0)
    /// @param _amount The amount of tokens to deposit
    function deposit(
        address _token,
        uint256 _amount
    ) external payable virtual;

    event Deposited(address indexed sender, address indexed token, uint256 amount, uint256 minted, uint256 entranceFee, uint256 fundmanagementFee);
    /// @notice Withdraw tokens or ETH from the DAO with a withdraw reference string
    /// @param _to The target address to send tokens or ETH
    function withdraw(
        address _to
    ) external virtual;

    event Withdrawn(address indexed token, address indexed to, uint256 amount, uint256 rageQuitFee, uint256 fundmanagementFee, uint256 performanceFee);

    function getManager() external virtual returns(address);

    function validateProposal(bytes memory _proposal, address _creator) public virtual view returns(bool);

}

/*
 * SPDX-License-Identifier:    MIT
 */

pragma solidity 0.8.10;

interface ITokenValueCalculator {
    function convert(address _token, uint256 _amount) external returns (uint256);
}

/*
 * SPDX-License-Identifier:    MIT
 */

pragma solidity 0.8.10;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "./IACLOracle.sol";

library ACLData {
    enum BulkOp { Grant, Revoke, Freeze }

    struct BulkItem {
        BulkOp op;
        bytes32 role;
        address who;
    }

    /// @notice Thrown if the function is not authorized
    /// @param here The contract containing the function
    /// @param where The contract being called
    /// @param who The address (EOA or contract) owning the permission
    /// @param role The role required to call the function
    error ACLAuth(address here, address where, address who, bytes32 role);

    /// @notice Thrown if the role was already granted to the address interacting with the target
    /// @param where The contract being called
    /// @param who The address (EOA or contract) owning the permission
    /// @param role The role required to call the function
    error ACLRoleAlreadyGranted(address where, address who, bytes32 role);

    /// @notice Thrown if the role was already revoked from the address interact with the target
    /// @param where The contract being called
    /// @param who The address (EOA or contract) owning the permission
    /// @param role The hash of the role identifier
    error ACLRoleAlreadyRevoked(address where, address who, bytes32 role);

    /// @notice Thrown if the address was already granted the role to interact with the target
    /// @param where The contract being called
    /// @param role The hash of the role identifier
    error ACLRoleFrozen(address where, bytes32 role);
}

/// @title The ACL used in the DAO contract to manage all permissions of a DAO.
/// @author Aragon Association - 2021
/// @notice This contract is used in the DAO contract and handles all the permissions of a DAO. This means it also handles the permissions of the processes or any custom component of the DAO.
contract ACL is Initializable {
    // @notice the ROOT_ROLE identifier used 
    bytes32 public constant ROOT_ROLE = keccak256("ROOT_ROLE");

    // "Who" constants
    address internal constant ANY_ADDR = address(type(uint160).max);

    // "Access" flags
    address internal constant UNSET_ROLE = address(0);
    address internal constant ALLOW_FLAG = address(2);
        
    // hash(where, who, role) => Access flag(unset or allow) or ACLOracle (any other address denominates auth via ACLOracle)
    mapping (bytes32 => address) internal authPermissions;
    // hash(where, role) => true(role froze on the where), false(role is not frozen on the where)
    mapping (bytes32 => bool) internal freezePermissions;

    // Events
    event Granted(bytes32 indexed role, address indexed actor, address indexed who, address where, IACLOracle oracle);
    event Revoked(bytes32 indexed role, address indexed actor, address indexed who, address where);
    event Frozen(bytes32 indexed role, address indexed actor, address where);

    /// @dev The modifier used within the DAO framework to check permissions.
    //       Allows to set ROOT roles on specific contract or on the main, overal DAO.
    /// @param _where The contract that will be called
    /// @param _role The role required to call the method this modifier is applied to
    modifier auth(address _where, bytes32 _role) {
        if(!(willPerform(_where, msg.sender, _role, msg.data) || 
            willPerform(address(this), msg.sender, _role, msg.data)))
            revert ACLData.ACLAuth({here: address(this), where: _where, who: msg.sender, role: _role});
        _;
    }

    /// @dev Init method to set the owner of the ACL
    /// @param _who The callee of the method
    function __ACL_init(address _who) internal onlyInitializing {
        _initializeACL(_who);
    }
    
    /// @dev Method to grant permissions for a role on a contract to an address
    /// @param _where The address of the contract
    /// @param _who The address (EOA or contract) owning the permission
    /// @param _role The hash of the role identifier
    function grant(address _where, address _who, bytes32 _role) external auth(_where, ROOT_ROLE) {
        _grant(_where, _who, _role);
    }

    /// @dev This method is used to grant access on a method of a contract based on a ACLOracle that allows us to have more dynamic permissions management.
    /// @param _where The address of the contract
    /// @param _who The address (EOA or contract) owning the permission
    /// @param _role The hash of the role identifier
    /// @param _oracle The ACLOracle responsible for this role on a specific method of a contract
    function grantWithOracle(address _where, address _who, bytes32 _role, IACLOracle _oracle) external auth(_where, ROOT_ROLE) {
        _grantWithOracle(_where, _who, _role, _oracle);
    }

    /// @dev Method to revoke permissions of an address for a role of a contract
    /// @param _where The address of the contract
    /// @param _who The address (EOA or contract) owning the permission
    /// @param _role The hash of the role identifier
    function revoke(address _where, address _who, bytes32 _role) external auth(_where, ROOT_ROLE) {
        _revoke(_where, _who, _role);
    }

    /// @dev Method to freeze a role of a contract
    /// @param _where The address of the contract
    /// @param _role The hash of the role identifier
    function freeze(address _where, bytes32 _role) external auth(_where, ROOT_ROLE) {
        _freeze(_where, _role);
    }

    /// @dev Method to do bulk operations on the ACL
    /// @param _where The address of the contract
    /// @param items A list of ACL operations to do
    function bulk(address _where, ACLData.BulkItem[] calldata items) external auth(_where, ROOT_ROLE) {
        for (uint256 i = 0; i < items.length; i++) {
            ACLData.BulkItem memory item = items[i];

            if (item.op == ACLData.BulkOp.Grant) _grant(_where, item.who, item.role);
            else if (item.op == ACLData.BulkOp.Revoke) _revoke(_where, item.who, item.role);
            else if (item.op == ACLData.BulkOp.Freeze) _freeze(_where, item.role);
        }
    }

    /// @dev This method is used to check if a callee has the permissions for. It is public to simplify the code within the DAO framework.
    /// @param _where The address of the contract
    /// @param _who The address (EOA or contract) owning the permission
    /// @param _role The hash of the role identifier
    /// @param _data The optional data passed to the ACLOracle registered.
    /// @return bool
    function willPerform(address _where, address _who, bytes32 _role, bytes memory _data) public returns (bool) {
        return _checkRole(_where, _who, _role, _data) // check if _who is eligible for _role on _where
            || _checkRole(_where, ANY_ADDR, _role, _data) // check if anyone is eligible for _role on _where
            || _checkRole(ANY_ADDR, _who, _role, _data); // check if _who is eligible for _role on any contract.
    }

    /// @dev This method is used to check if a given role on a contract is frozen
    /// @param _where The address of the contract
    /// @param _role The hash of the role identifier
    /// @return bool Return true or false depending if it is frozen or not
    function isFrozen(address _where, bytes32 _role) public view returns (bool) {
        return freezePermissions[freezeHash(_where, _role)];
    }

    /// @dev This method is internally used to grant the ROOT_ROLE on initialization of the ACL
    /// @param _who The address (EOA or contract) owning the permission
    function _initializeACL(address _who) internal {
        _grant(address(this), _who, ROOT_ROLE);
    }

    /// @dev This method is used in the public grant method of the ACL
    /// @param _where The address of the contract
    /// @param _who The address (EOA or contract) owning the permission
    /// @param _role The hash of the role identifier
    function _grant(address _where, address _who, bytes32 _role) internal {
        _grantWithOracle(_where, _who, _role, IACLOracle(ALLOW_FLAG));
    }

    /// @dev This method is used in the internal _grant method of the ACL
    /// @param _where The address of the contract
    /// @param _who The address (EOA or contract) owning the permission
    /// @param _role The hash of the role identifier
    /// @param _oracle The ACLOracle to be used or it is just the ALLOW_FLAG
    function _grantWithOracle(address _where, address _who, bytes32 _role, IACLOracle _oracle) internal {
        if(isFrozen(_where, _role)) revert ACLData.ACLRoleFrozen({where: _where, role: _role});

        bytes32 permission = permissionHash(_where, _who, _role);
        if(authPermissions[permission] != UNSET_ROLE)
            revert ACLData.ACLRoleAlreadyGranted({where: _where, who: _who, role: _role});
        authPermissions[permission] = address(_oracle);

        emit Granted(_role, msg.sender, _who, _where, _oracle);
    }

    /// @dev This method is used in the public revoke method of the ACL
    /// @param _where The address of the contract
    /// @param _who The address (EOA or contract) owning the permission
    /// @param _role The hash of the role identifier
    function _revoke(address _where, address _who, bytes32 _role) internal {
        if(isFrozen(_where, _role)) revert ACLData.ACLRoleFrozen({where: _where, role: _role});

        bytes32 permission = permissionHash(_where, _who, _role);
        if(authPermissions[permission] == UNSET_ROLE) revert ACLData.ACLRoleAlreadyRevoked({where: _where, who: _who, role: _role});
        authPermissions[permission] = UNSET_ROLE;

        emit Revoked(_role, msg.sender, _who, _where);
    }

    /// @dev This method is used in the public freeze method of the ACL
    /// @param _where The address of the contract
    /// @param _role The hash of the role identifier
    function _freeze(address _where, bytes32 _role) internal {
        bytes32 permission = freezeHash(_where, _role);
        if(freezePermissions[permission]) revert ACLData.ACLRoleFrozen({where: _where, role: _role});
        freezePermissions[permission] = true;

        emit Frozen(_role, msg.sender, _where);
    }

    /// @dev This method is used in the public willPerform method of the ACL.
    /// @param _where The address of the contract
    /// @param _who The address (EOA or contract) owning the permission
    /// @param _role The hash of the role identifier
    /// @param _data The optional data passed to the ACLOracle registered.
    /// @return bool
    function _checkRole(address _where, address _who, bytes32 _role, bytes memory _data) internal returns (bool) {
        address accessFlagOrAclOracle = authPermissions[permissionHash(_where, _who, _role)];
        
        if (accessFlagOrAclOracle == UNSET_ROLE) return false;
        if (accessFlagOrAclOracle == ALLOW_FLAG) return true;

        // Since it's not a flag, assume it's an ACLOracle and try-catch to skip failures
        try IACLOracle(accessFlagOrAclOracle).willPerform(_where, _who, _role, _data) returns (bool allowed) {
            if (allowed) return true;
        } catch { }
        
        return false;
    }

    /// @dev This internal method is used to generate the hash for the authPermissions mapping based on the target contract, the address to grant permissions, and the role identifier.
    /// @param _where The address of the contract
    /// @param _who The address (EOA or contract) owning the permission
    /// @param _role The hash of the role identifier
    /// @return bytes32 The hash of the permissions
    function permissionHash(address _where, address _who, bytes32 _role) internal pure returns (bytes32) {
        return keccak256(abi.encodePacked("PERMISSION", _who, _where, _role));
    }

    /// @dev This internal method is used to generate the hash for the freezePermissions mapping based on the target contract and the role identifier.
    /// @param _where The address of the contract
    /// @param _role The hash of the role identifier
    /// @return bytes32 The freeze hash used in the freezePermissions mapping
    function freezeHash(address _where, bytes32 _role) internal pure returns (bytes32) {
        return keccak256(abi.encodePacked("FREEZE", _where, _role));
    }
}

/*
 * SPDX-License-Identifier:    MIT
 */

pragma solidity 0.8.10;

/// @title The IACLOracle to have dynamic permissions
/// @author Aragon Association - 2021
/// @notice This contract used to have dynamic permissions as for example that only users with a token X can do Y.
interface IACLOracle {
    // @dev This method is used to check if a callee has the permissions for.
    // @param _where The address of the contract
    // @param _who The address of a EOA or contract to give the permissions
    // @param _role The hash of the role identifier
    // @param _data The optional data passed to the ACLOracle registered.
    // @return bool
    function willPerform(address _where, address _who, bytes32 _role, bytes calldata _data) external returns (bool allowed);
}

import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";

library ECDSAExternal {
    function recover(bytes32 hash, bytes memory signature) external pure returns (address) {
        return ECDSA.recover(hash, signature);
    }
}

interface IPermit2 {
    function DOMAIN_SEPARATOR() external view returns (bytes32);
}

library Permit2Lib {
    // Same on all networks
    IPermit2 constant PERMIT2 = IPermit2(0x000000000022D473030F116dDEE9F6B43aC78BA3);

    struct PermitDetails {
        address token;
        uint160 amount;
        uint48 expiration;
        uint48 nonce;
    }

    struct PermitSingle {
        PermitDetails details;
        address spender;
        uint256 sigDeadline;
    }

    bytes32 public constant PERMIT_DETAILS_TYPEHASH =
        keccak256("PermitDetails(address token,uint160 amount,uint48 expiration,uint48 nonce)");
    bytes32 public constant PERMIT_SINGLE_TYPEHASH = keccak256(
        "PermitSingle(PermitDetails details,address spender,uint256 sigDeadline)PermitDetails(address token,uint160 amount,uint48 expiration,uint48 nonce)"
    );

    bytes4 private constant EIP1271_MAGIC_VALUE = 0x1626ba7e;

    function hashData(address _token, uint160 amount, uint48 expiration, uint48 nonce, address spender, uint256 deadline) external view returns (bytes32) {
        PermitDetails memory details = PermitDetails(_token, amount, expiration, nonce);
        PermitSingle memory single = PermitSingle(details, spender, deadline);

        return hashData(single);
    }

    function hashData(PermitSingle memory permitSingle) private view returns (bytes32) {
        return hashPermit2(permitSingle);
    }

    function hashPermit2(PermitSingle memory permitSingle) private view returns (bytes32) {
        bytes32 domainSeparator = PERMIT2.DOMAIN_SEPARATOR();

        bytes32 detailsHash = keccak256(abi.encode(PERMIT_DETAILS_TYPEHASH, permitSingle.details));
        bytes32 permitSingleHash = keccak256(abi.encode(PERMIT_SINGLE_TYPEHASH, detailsHash, permitSingle.spender, permitSingle.sigDeadline));
        return keccak256(abi.encodePacked("\x19\x01", domainSeparator, permitSingleHash));
    }
}

pragma solidity 0.8.10;

interface IAspisRegistry {

    function register(string memory, address, address, address) external;

    function getDecoder(address) external returns(address);

    function getAspisSupportedTradingTokens() external returns(address[] memory);

    function getAspisSupportedTradingProtocols() external returns(address[] memory);

    function isAspisSupportedTradingToken(address) external returns(bool);

    function isAspisSupportedTradingProtocol(address) external returns(bool);
}